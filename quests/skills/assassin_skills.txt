//===== rAthena Script ======================================= 
//= Assassin Skills Quests
//===== By: ================================================== 
//===== Current Version: ===================================== 
//= 1.0
//===== Compatible With: ===================================== 
//= Auriga Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Quests for skills: Venom Knife, Sonic Acceleration
//===== Additional Comments: ================================= 
//============================================================

//-----------------------------------------------
//==== ソニックアクセラレーション - AS_SONICACCEL
//	■ SKILL_SONICACCEL   -> 0～4
//-----------------------------------------------
in_moc_16.gat,23,27,5	script	アサシン	885,{
	if(getbaseclass(Class,2) != CLASS_AS) {
		mes "[アサシン]";
		mes "…………";
		close;
	}
	if(getskilllv(1003)) {
		mes "[イスミル]";
		mes "ソニックアクセラレーションは";
		mes "使ってみたかしら？";
		mes "覚える為にした努力が";
		mes "無駄にならないといいわね。";
		close;
	}
	if(getskilllv(1004) == 0) {
		mes "[アサシン]";
		mes "ごきげんよう。ギルドの方ね。";
		mes "連絡を受けていらしたの？";
		emotion 18;
		next;
		menu "連絡……？",-;
		mes "[アサシン]";
		mes "あら……";
		mes "分からないようね。";
		mes "横にいらっしゃるキルティンさんと";
		mes "お話ししてみれば分かりますわ。";
		mes "フフッ。";
		close;
	}
	switch(SKILL_SONICACCEL) {
	case 0:
		mes "[イスミル]";
		mes "ごきげんよう。";
		mes "私は貴方と同じ、アサシンの";
		mes "イスミルと申しますわ。";
		mes "キルティンさんと一緒にギルドの";
		mes "技術教育担当をしているのよ。";
		next;
		mes "[イスミル]";
		mes "それでは……";
		mes strcharinfo(0)+ "さん。";
		mes "基本的なソニックアクセラレーションの";
		mes "説明はキルティンさんに";
		mes "伺っているものと思いますが……";
		next;
		mes "[イスミル]";
		mes "また説明が必要であれば";
		mes "お話して差し上げる事ができますが";
		mes "どうなさいますか？";
		next;
		while(1) {
			if(select("また説明を聞く。","もう分かっています。")==2)
				break;
			mes "[イスミル]";
			mes "わかりました。";
			mes "では説明をいたしますわ。";
			mes "ソニックアクセラレーションは";
			mes "^FF0000ソニックブローを更に";
			mes "強くしてくれる補助スキル^000000だと";
			mes "いえるわ。";
			next;
			mes "[イスミル]";
			mes "既存のソニックブローを";
			mes "使ってみれば分かるとは思うけど……";
			mes "敵に致命傷を与えるという事は";
			mes "かなり難しい事なのよ。";
			next;
			mes "[イスミル]";
			mes "その上、素早く8連続打撃を";
			mes "しなければならないわけで、";
			mes "命中率もお世辞にも良いとは";
			mes "いえないし……。";
			next;
			mes "[イスミル]";
			mes "狙った部分を攻撃する事が難しいので、";
			mes "スキルが命中したとしても、";
			mes "大きなダメージを与えにくいのよ。";
			next;
			mes "[イスミル]";
			mes "もちろんこれは人によって違うけど、";
			mes "多くの人はそうだという事ね。";
			next;
			mes "[イスミル]";
			mes "貴方は、その短所を補う";
			mes "ソニックアクセラレーションを";
			mes "学ぶ事によって、より強力で正確な";
			mes "ソニックブローで、相手に致命傷を";
			mes "与える事ができる……。";
			next;
			mes "[イスミル]";
			mes "ああ……考えだけでもとても魅力的。";
			mes "このようなスキルを開発した、";
			mes "ギルドマスター様はやはり";
			mes "とても素敵ですわ……。";
			emotion 3;
			next;
			mes "[イスミル]";
			mes "スキルに対して説明できる事は";
			mes "このくらいですわね。";
			mes "これ以上は直接使ってみながら";
			mes "お教えしましょう。";
			next;
		}
		mes "[イスミル]";
		mes "それではすぐに";
		mes "『ソニックアクセラレーション』を";
		mes "お教えしましょう。";
		next;
		mes "[イスミル]";
		mes "スキルを学ぶ準備ができたかしら？";
		next;
		menu "はい！",-;
		mes "[イスミル]";
		mes "よろしいですわ。";
		mes "スキルを学ぶ為には、";
		mes "『ギルドの為になる仕事をする』";
		mes "というのが教える条件よ。";
		next;
		if(getskilllv(136) < 1) {
			mes "[イスミル]";
			mes "……って、ちょっと。";
			mes "貴方、アサシンの基本中の基本スキル";
			mes "『ソニックブロー』が出来ていない";
			mes "のではなくて？";
			mes "貴方、アサシンの基本中の基本スキル";
			mes "『ソニックブロー』無しでは";
			mes "習得する事は不可能なのよ。";
			next;
			mes "[イスミル]";
			mes "出直してらして。";
			close;
		}
		mes "[イスミル]";
		mes "あなたがする仕事は……";
		next;
		//ピラミッドルート未実装
		//set SKILL_SONICACCEL,rand(1,3);
		set SKILL_SONICACCEL,rand(1,2);
		switch(SKILL_SONICACCEL) {
		case 1:
			mes "[イスミル]";
			mes "スフィンクスダンジョンに行って、";
			mes "宝物に値する品物を一つ";
			mes "持ってきてください。";
			mes "あそこには価値の高い宝物がたくさん";
			mes "あるけど、強力なモンスター達が";
			mes "多くて、簡単には近づけないのよ。";
			next;
			mes "[イスミル]";
			mes "すぐに行ってらっしゃい。";
			mes "スフィンクスダンジョンで宝物を";
			mes "持ってくるのですよ。";
			mes "もしかしたら『棺』の中に";
			mes "宝物があるかも知れないわね。";
			break;
		case 2:
			mes "[イスミル]";
			mes "スフィンクスダンジョンに行って、";
			mes "宝物に値する品物を一つ";
			mes "持ってきてください。";
			mes "あそこには価値の高い宝物がたくさん";
			mes "あるけど、強力なモンスター達が";
			mes "多くて、簡単には近づけないのよ。";
			next;
			mes "[イスミル]";
			mes "すぐに行ってらっしゃい。";
			mes "スフィンクスダンジョンで宝物を";
			mes "持ってくるのですよ。";
			mes "もしかしたら『石像』の近くに";
			mes "宝物があるかも知れないわね。";
			break;
		}
		next;
		mes "[イスミル]";
		mes "貴方がアサシンとしてこの仕事を";
		mes "やりこなす事ができるのか、";
		mes "それともできないのか見届けるわ。";
		mes "ソニックアクセラレーションをお教え";
		mes "するかどうかはその後に判断するわね。";
		mes "フフ……できるだけ急いでくださいね。";
		close;
	case 1:
	case 2:
	case 3:
		mes "[イスミル]";
		mes "どうかしら？";
		mes "与えられた仕事は進んでる？";
		mes "あまり長くは待ちませんわ。";
		mes "早く終わらせてらっしゃい。";
		close;
	case 4:
		mes "[イスミル]";
		mes "どうですか？";
		mes "与えられた仕事は進んでるかしら？";
		next;
		if(countitem(726) < 1 && countitem(723) < 1 && countitem(720) < 1) {
			mes "[イスミル]";
			mes "あら、財宝はどうしたの？";
			mes "……………………";
			mes "無くしちゃったみたいね。";
			next;
			mes "[イスミル]";
			mes "……仕方ないですわ。";
			mes "あなたが手に入れた財宝……";
			mes "ルビー、サファイア";
			mes "アクアマリンのどれかを";
			mes "持ってきなさい。";
			mes "手段は問わないわ。";
			close;
		}
		menu "これを!!",-;
		mes "[イスミル]";
		mes "この輝きは……";
		mes "ああ……";
		mes "なんて美しい……";
		next;
		mes "[イスミル]";
		mes "よろしいですわ。";
		mes "これがピラミッドやスフィンクスで";
		mes "手に入れたかどうかは";
		mes "特に重要ではないのよ。";
		mes "とても綺麗なその宝物は";
		mes "貴方が持ってなさい。";
		next;
		mes "[イスミル]";
		mes "もう実力は見せてくれたから宝物は";
		mes "必要ないわ。";
		mes "フフ……";
		next;
		mes "[イスミル]";
		mes "実はこれはギルドで指示された事では";
		mes "なかったのよ。";
		mes "ただ貴方の力を見たかったのです。";
		mes "それでは約束どおり今ここで";
		mes "^0000FFソニックアクセラレーション^000000を";
		mes "教えて差し上げますわ。";
		next;
		mes "[イスミル]";
		mes "私のお手本をしっかりと見て、";
		mes "教えて差し上げる方法通りに";
		mes "実際にやってみるのよ。";
		mes "必ずできるようになりますわ。";
		next;
		mes "[イスミル]";
		mes "よろしくて？";
		mes "それでは……";
		next;
		mes "[イスミル]";
		mes "まず、ソニックブローを使う前に";
		mes "…………";
		mes "足をこうして……";
		mes "そして、こう！";
		misceffect 121;
		next;
		mes "[イスミル]";
		mes "ちゃんと見ていたかしら？";
		mes "この部分が重要なのよ。";
		mes "腕と手の位置を……こう……";
		mes "そしてこうするのよ！";
		misceffect 121;
		next;
		mes "[イスミル]";
		mes "さあ、やってみてください。";
		next;
		misceffect 121,"";
		next;
		mes "[イスミル]";
		mes "もう一度！";
		next;
		misceffect 121,"";
		next;
		mes "[イスミル]";
		mes "うーん、ちょっと違うわね。";
		mes "もう一度やってみてください。";
		next;
		misceffect 121,"";
		next;
		mes "[イスミル]";
		mes "…………";
		next;
		mes "[イスミル]";
		mes "最後にもう一度！";
		next;
		misceffect 121,"";
		next;
		mes "[イスミル]";
		mes "素晴らしいですわ！";
		mes "それで良いのです！";
		set SKILL_SONICACCEL,0;
		skill 1003,1,0;
		next;
		mes "[イスミル]";
		mes "もう理解できたわね？";
		mes "では理論とお手本は終了よ。";
		mes "貴方が真のアサシンなら";
		mes "これくらいで十分のはずですわ。";
		mes "忘れないように。";
		next;
		mes "[イスミル]";
		mes "後は実際に実戦で使ってごらんなさい。";
		mes "実戦経験が一番の練習になりますわ。";
		mes "フフ……私は次のアサシンの為の";
		mes "準備をしなければいけないので、";
		mes "これで失礼するわ。";
		close;
	}
}

in_sphinx2.gat,259,213,0	script	古い棺#1	139,7,8,{
	if(SKILL_SONICACCEL != 1)
		end;
	mes "とても古そうな棺がある。";
	mes "棺のふたが少し割れていて";
	mes "その中で光を反射する輝く";
	mes "何かがあるようだ。";
	misceffect 71;
	next;
	mes "どうする？";
	next;
	while(1) {
		switch(select("手を入れてみる","もうちょっと近くで確認をする","ふたを開いて確認する","棺を横に倒す","無視する")) {
		case 1:
			mes "あなたは手を入れた。";
			mes "そして腕を深く入れて輝く何かを";
			mes "取ろうと手を伸ばした。";
			next;
			mes "……………………";
			mes "……………………";
			mes "……………………";
			mes "……………………";
			next;
			if(strnpcinfo(2) != 2 || rand(3)) {
				mes "^FF0000!!!!!!!!!!";
				mes "何かがあなたの手に噛み付いた!!!!^000000";
				sc_start SC_POISON,30000,0;
				emotion 23,"";
				close;
			}
			mes "硬い何かが手に触れた。";
			mes "あなたはそれをしっかり握りしめて";
			mes "手を取り出した。";
			next;
			mes "取り出した手を開くと";
			mes "スフィンクスの財宝らしい";
			mes "宝石を手に入れた。";
			set SKILL_SONICACCEL,4;
			getitem 726,1;
			close;
		case 2:
			mes "あなたは棺の割れた隙間に";
			mes "顔を近付けた。";
			next;
			mes "……………………";
			mes "……………………";
			mes "……………………";
			next;
			mes "暗い棺の中で何か";
			mes "動いているのが見える。";
			mes "それ以上は暗くて見えない。";
			sc_start SC_BLIND,30000,0;
			break;
		case 3:
			mes "棺のふたは不思議な力が働いて";
			mes "びくともしない。";
			sc_start SC_CURSE,30000,0;
			break;
		case 4:
			mes "棺を横に倒そうとしたが";
			mes "まったく動かない。";
			break;
		case 5:
			close;
		}
		next;
	}
}
in_sphinx2.gat,212,267,0	duplicate(古い棺#1)	古い棺#2	139,5,6

in_sphinx2.gat,13,163,0	script	石像#1	139,7,7,{
	if(SKILL_SONICACCEL != 2)
		end;
	misceffect 71;
	mes "古そうな石像が立っている。";
	mes "あちこち装飾が剥がれていて";
	mes "今にも倒れそうだ。";
	next;
	mes "石像の周りはぽこんとくぼんでいる。";
	mes "よく見ると石像の足下部に";
	mes "輝く何かがある。";
	misceffect 71;
	next;
	mes "どうする？";
	next;
	if(select("近づいて確認する","無視する")==2) {
		mes "…………";
		close;
	}
	mes "くぼんだ所へ足を踏み出すと";
	mes "底から黄色い煙が噴き出した。";
	next;
	if(strnpcinfo(2) != 1 || rand(3)) {
		mes "あなたは";
		mes "煙で呼吸が苦しくなった。";
		mes "あなたは意識を失った。";
		sc_start SC_SLEEP,30000,0;
		close;
	}
	mes "あなたは";
	mes "息を止めて、";
	mes "素早く石像の足元部を確認した。";
	next;
	if(strnpcinfo(2) != 1) {
		mes "輝く何かは折れて捨てられた";
		mes "剣の刃だった。";
		emotion 23,"";
		close;
	}
	mes "スフィンクスの財宝らしい";
	mes "宝石を手に入れた。";
	set SKILL_SONICACCEL,4;
	getitem 723,1;
	close;
}
in_sphinx2.gat,13,146,0	duplicate(石像#1)	石像#2	139,7,7


//-----------------------------------------------
//==== ベナムナイフ - AS_VENOMKNIFE
//-----------------------------------------------
in_moc_16.gat,14,27,5	script	アサシン	884,{
	if(getbaseclass(Class,2) != CLASS_AS) {
		mes "[アサシン]";
		mes "……………………";
		close;
	}
	if(getskilllv(1003) && getskilllv(1004)) {
		mes "[キルティン]";
		mes "お、新しいスキルを両方習得したか。";
		mes "なかなかやるな。";
		mes "君はもっと強くなるだろうよ。";
		mes "これからもギルドのために";
		mes "もっと力を尽くしてくれよ。";
		close;
	}
	if(getskilllv(1004)) {
		mes "[キルティン]";
		mes "どうだい、";
		mes "^9C009Cベナムナイフ^000000は使っているかい？";
		mes "使う時は気を付けてくれよ。";
		mes "毒付きナイフが無いと使えないからな。";
		mes "あとは、間違って投げてしまって";
		mes "無駄使いするような事がないようにな。";
		close;
	}
	mes "[キルティン]";
	mes "ん……、おう、君は";
	mes strcharinfo(0)+ "じゃないか？";
	mes "久しぶりにギルドに来たな！";
	next;
	mes "[キルティン]";
	mes "ちょうどよかった。";
	mes "今、あちこちで活動している";
	mes "アサシンギルド員たちを召集しようと";
	mes "していたところだ。";
	next;
	menu "ギルドで何か起こったんですか？",-;
	mes "[キルティン]";
	mes "おう、その通りだ。";
	mes "それも、おもしろい事がな……";
	next;
	mes "[キルティン]";
	mes "フフフ……いいか、驚くなよ？";
	mes "ギルド員たちを召集する理由とは！";
	mes "ギルドマスターが編み出した";
	mes "アサシンの新しいスキルを";
	mes "伝授するためなんだよ！";
	next;
	menu "新しいスキル？",-;
	mes "[キルティン]";
	mes "ふふん、随分と目を輝かせてるな。";
	mes "そう、新しいスキルだ。";
	next;
	menu "詳しく！",-;
	mes "[キルティン]";
	mes "まあそう急かすなって！";
	mes "2つのスキルが新たに開発されたのさ。";
	mes "1つ目は^0000FFソニックブローの改良型^000000。";
	mes "2つ目は^9C009Cベナムナイフ^000000という";
	mes "遠距離攻撃スキルだ。";
	mes "どんなスキルなのか知りたいだろう？";
	next;
	mes "[キルティン]";
	mes "じゃあ2つの中でどのスキルから";
	mes "説明して欲しい？";
	mes "それとも説明はいらないか？";
	next;
	while(1) {
		switch(select("^0000FFソニックブローの改良型^000000","^9C009Cベナムナイフ^000000","説明は結構です。")) {
		case 1:
			mes "[キルティン]";
			mes "俺たちアサシンギルド員の";
			mes "代表的なスキルと言えば勿論";
			mes "^0000FFソニックブロー^000000だ。";
			next;
			mes "[キルティン]";
			mes "君も当然知っているとは思うが";
			mes "ソニックブローは相手を一気に";
			mes "8連続攻撃するスキルだ。";
			mes "だが、高い集中力と器用さを必要と";
			mes "するから素早い敵を攻撃するのは";
			mes "なかなか難しいもんだ。";
			next;
			mes "[キルティン]";
			mes "ギルドマスターはいつもその問題を";
			mes "解決しようと思って、長い間";
			mes "研究を続けてきたようでね。";
			mes "ついにこの欠点を補う解決策を";
			mes "見出したわけだ。";
			next;
			mes "[キルティン]";
			mes "その解決策とは、";
			mes "ギルドマスター直々に編み出した";
			mes "^0000FFソニックアクセラレーション^000000";
			mes "というスキルだ。";
			next;
			mes "[キルティン]";
			mes "ソニックアクセラレーションとは……";
			mes "ソニックブローを使う時に";
			mes "相手の弱点やスキを見極めるための";
			mes "鋭い目を身につける事ができる";
			mes "スキルなのさ。";
			next;
			mes "[キルティン]";
			mes "第一に、";
			mes "ソニックアクセラレーションは";
			mes "ソニックブローを使う時に";
			mes "今までよりも早く正確な打撃を";
			mes "バックアップしてくれるスキルである。";
			next;
			mes "[キルティン]";
			mes "第二に、";
			mes "ソニックアクセラレーションは";
			mes "敵を攻撃するスキルというよりは";
			mes "自分自身がソニックブローを使うのに";
			mes "最良の条件を整えてくれる";
			mes "スキルだと言える。";
			next;
			mes "[キルティン]";
			mes "ソニックアクセラレーションを習得して";
			mes "ソニックブローを使えば";
			mes "その効果は抜群だ。";
			next;
			mes "[キルティン]";
			mes "習得したいなら^9C009Cベナムナイフ^000000を";
			mes "習得してから、俺の横に居る";
			mes "『麗しのアサシンクロス』、";
			mes "イスミルに話してみな。";
			next;
			continue;
		case 2:
			mes "[キルティン]";
			mes "よし。^9C009Cベナムナイフ^000000は";
			mes "俺たちアサシンに不足している";
			mes "遠距離攻撃を、少しでも";
			mes "補う事ができるようなスキルとして";
			mes "編み出されたものだ。";
			next;
			mes "[キルティン]";
			mes "石投げ、グリムトゥース、";
			mes "ソウルブレイカーといったものがあるが";
			mes "使うにはかなりの制約があるだろう？";
			mes "だからこそ、このスキルは";
			mes "役立つのさ。";
			next;
			mes "[キルティン]";
			mes "スキルのインベナムと";
			mes "武器のナイフを使うんだ。";
			mes "インベナムをナイフに";
			mes "ぶち込んだ後、相手にそのナイフを";
			mes "投げて刺す。するとナイフに";
			mes "付着しているインベナムの毒が";
			next;
			mes "[キルティン]";
			mes "相手の体内に入り込む……";
			mes "そんなスキルだな。";
			mes "誰かを遠くから毒で";
			mes "処理しなければならない時に有効だ。";
			mes "ククククク……";
			next;
			continue;
		case 3:
			break;
		}
		break;
	}
	mes "[キルティン]";
	mes "よし、なら君はどのスキルを学びたい？";
	mes "とはいえ俺は^9C009Cベナムナイフ^000000しか";
	mes "教えてやれないが。";
	mes "何、^636363ナイフにインベナムをぶち込む^000000";
	mes "方法だけ分かればいいのさ。";
	mes "今すぐ教えてやれるが、どうする？";
	next;
	while(1) {
		if(select("教えてもらう。","今は教わらない。")==1)
			break;
		mes "[キルティン]";
		mes "何ぃ？　生意気な奴だ……";
		mes "ギルドマスターの努力を";
		mes "無駄にしようってのか？";
		mes "今すぐ教わりたいと言うんだ！";
		next;
	}
	mes "[キルティン]";
	mes "よし！";
	mes "じゃあ教えてやるからよく聞けよ。";
	mes "まず^9C009Cベナムナイフ^000000を使う前に";
	mes "^0000FFナイフ^000000を持っていなければならない。";
	mes "それでもってだ……";
	next;
	misceffect 129;
	mes "[キルティン]";
	mes "インベナムをこうして……";
	next;
	mes "[キルティン]";
	mes "……後は……こうやって";
	mes "こう……あらよっと。";
	mes "簡単だろう？";
	mes "一回やってみな。";
	next;
	misceffect 129,"";
	next;
	mes "[キルティン]";
	mes "そう！";
	mes "そうやるんだ。";
	next;
	mes "[キルティン]";
	mes "なかなか見込みがあるな。";
	mes "だがまあ、実際に";
	mes "使ってみるのが一番だと思うぜ。";
	mes "分かったつもりでいるよりもな。";
	mes "ククク……";
	next;
	mes "[キルティン]";
	mes "じゃあ精進しろよ。";
	mes "アサシンギルドの名誉のために……";
	skill 1004,1,0;
	close;
}

in_moc_16.gat,22,20,7	shop	陰気な商人	880,1771:-1