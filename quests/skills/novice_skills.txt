//===== rAthena Script ======================================= 
//= Novice Skills Quests
//===== By: ================================================== 
//===== Current Version: ===================================== 
//= 1.0
//===== Compatible With: ===================================== 
//= Auriga Project
//===== Description: ========================================= 
//= Quest for skills: First Aid, Trick Dead
//===== Additional Comments: ================================= 
//============================================================ 
//-----------------------------------------------
//==== 応急処置 - NV_FIRSTAID
//-----------------------------------------------
prt_in.gat,234,133,4	script	ナミ	66,{
	if(Upper == UPPER_HIGH) {
		mes "[ナミ]";
		mes "あ、ちょうどいい所へ！";
		mes "私は看護教師志望の実習生、";
		mes "ナミといいます。";
		mes "よろしければ、実習の機会を";
		mes "いただけませんか？";
		next;
		if(select("うなづく","断る") == 2) {
			mes "[ナミ]";
			mes "あらあら。";
			mes "それは残念です……";
			close;
		}
		mes "[ナミ]";
		mes "ありがとうございます！";
		mes "それじゃあさっそく……";
		mes "チョチョイのチョイ！";
		mes "はい、これが応急手当です！";
		skill 142,1,0;
		next;
		mes "[ナミ]";
		mes "どうです？";
		mes "応急手当の仕方を";
		mes "思い出しましたか？";
		mes "ふふ、でもあなた程の人なら";
		mes "応急手当ぐらい";
		mes "お茶のこさいさいですね。";
		next;
		mes "[ナミ]";
		mes "それでは、";
		mes "ご協力ありがとうございました！";
		close;
	}
	if(getskilllv(142)) {
		mes "[ナミ]";
		mes "最近看護教師資格を取ろうと";
		mes "熱心に勉強しているのに";
		mes "成果があまり良くないんです…";
		mes "なんと言ってもやはり";
		mes "実習不足なようです…";
		next;
		mes "[ナミ]";
		mes "もし一次職以上に就いていたり、";
		mes "ノービスでも";
		mes "^3355FFJOBLV4以上^000000の方には";
		mes "実習のお手伝いをお願いしています。";
		mes "ふふふ…";
		close;
	}
	if(Job == Job_Novice && (JobLevel < 4 && BaseLevel < 12)) {
		mes "[ナミ]";
		mes "… z ｚ Z";
		mes "間違えました！先生！";
		mes "…？";
		mes "はっ!?";
		mes "- キョロキョロ -";
		close;
	}
	mes "[ナミ]";
	mes "あ、私は看護教師を志望して";
	mes "教育実習をしています。";
	mes "実習の機会をくださるの";
	mes "ですか…？";
	next;
	if(select("会話を続ける","コソコソと退く")==2) {
		mes "[ナミ]";
		mes "む？どこへ行くんですか！";
		mes "さぁこちらに来て注射を…";
		mes "クックック…";
		mes "はい、患者さ〜ん";
		close;
	}
	switch(SKILL_AID) {
	case 0:
		mes "[ナミ]";
		mes "付き合ってくださって感謝します！";
		mes "最善をつくし、誠心誠意";
		mes "心を込めてお教え致します。";
		mes "うふふ…";
		next;
		mes "[ナミ]";
		mes "私がお教えする内容は、";
		mes "^3355FF「応急手当」^000000に関するものです。";
		mes "特別な装備や機械などは使わず";
		mes "簡単なアイテム、すなわち";
		mes "布切れとか、安めの";
		mes "ポーション等を利用して";
		next;
		mes "[ナミ]";
		mes "応急処置を施し傷を処理 -";
		mes "多少の体力を回復する";
		mes "技術を習得できるようにして";
		mes "差し上げたいと思います。";
		mes "実習道具が不足していますので";
		mes "準備を手伝っていただけますか？";
		next;
		callsub L_Routine,1;
		next;
		mes "[ナミ]";
		mes "私たちが今から実習するのに";
		mes "必要な物ですから";
		mes "必ず用意して来てくださいね。";
		set SKILL_AID,1;
		close;
	case 1:
		callsub L_Routine,1;
		close;
	case 2:
		if(countitem(507)<3 || countitem(705)<3) {
			callsub L_Routine,0;	//必要アイテムの提示のみ
			close;
		}
		delitem 507,3;
		delitem 705,3;
		mes "[ナミ]";
		mes "おかえりなさい〜 準備を";
		mes "整えるのに苦労されたでしょう。";
		mes "他の物はまだしも、プロンテラ";
		mes "看護将校様の気むずかしい性格には";
		mes "よほど苦労なさった様子…";
		mes "ふふ…";
		next;
		mes "[ナミ]";
		mes "さ、それでは実習を始めます。";
		mes "応急手当は自分のSPを";
		mes "消費してHPを回復する";
		mes "技術です。^3355FFSP3^000000ほどを";
		mes "消費して^3355FF5^000000ほど^3355FFHP^000000を回復します。";
		mes "治療に必要な薬品を作り、";
		next;
		mes "[ナミ]";
		mes "傷を手当てする方法は…";
		mes "…";
		mes "チョチョイのチョイ！と";
		mes "こんな感じです。簡単でしょ？";
		next;
		mes "[ナミ]";
		mes "もう少し詳しく教えると、";
		mes "ポーションを大さじ１杯塗って";
		mes "赤いハーブを指の大きさに";
		mes "切ってこうしてああして";
		mes "ジャジャーン!!";
		next;
		skill 142,1,0;
		set SKILL_AID,0;
		mes "[ナミ]";
		mes "はい、できましたー！";
		mes "もう立派な手当てができますね。";
		mes "それではこの技術を";
		mes "よく活用してくださいね。";
		mes "さようなら〜";
		close;
	}
L_Routine:	//arg(0) -> 0or1
	mes "[ナミ]";
	mes "文字通り応急的な処置だから";
	mes "準備する物も手近な物ですよ。";
	mes "それでは次のアイテムです…";
	mes "^3355FF「赤いハーブ」^000000";
	mes "^3355FF「クローバー」^000000をそれぞれ^3355FF3枚づつ^000000";
	mes "と^3355FF「消毒処理した包帯」^000000が必要です。";
	if(getarg(0)==0)
		return;
	next;
	mes "[ナミ]";
	mes "消毒処理した包帯というのは、ある";
	mes "モンスターから取った「腐った包帯」を";
	mes "消毒液に漬けることにより綺麗に消毒";
	mes "処理したものですが、";
	mes "どんなモンスターや道具店からも";
	mes "直接手に入れることはできない";
	next;
	mes "[ナミ]";
	mes "物なのです…";
	mes "プロンテラ城内部の東2 階の";
	mes "どこかにいる看護将校様が";
	mes "いつもいっぱい備蓄していて、";
	mes "その方に申し出れば";
	mes "くださるはずです…";
	return;
}

//-----------------------------------------------
//==== 死んだふり - NV_TRICKDEAD(応急処置なしは不可)
//-----------------------------------------------
prt_in.gat,73,87,4	script	プロンテラ騎士団員	65,{
	if(Job != Job_Novice || JobLevel < 7 || getskilllv(142) < 1 || getskilllv(143)) {
		mes "[ブルーマー]";
		mes "いやぁ本当に昔を思い出すな…";
		mes "特にノービスだった頃の。";
		mes "苦労したな…その時は本当に";
		mes "大変だったが、やっと今の地位を";
		mes "築いたおかげで少しの余裕できて";
		mes "^3355FF職業レベル 7 以上^000000";
		next;
		mes "[ブルーマー]";
		mes "^3355FF+応急手当スキル習得済み^000000";
		mes "の^3355FFノービス^000000達に技を教える";
		mes "ことができるようになった。";
		next;
		mes "[ブルーマー]";
		mes "お前らのノービス仲間で";
		mes "このくらいまで育った奴がいたら";
		mes "私のことを紹介してやってくれ。";
		mes "そいつらの役に立つだろう。";
		close;
	}
	mes "[ブルーマー]";
	mes "そう！ご覧のとおり俺は";
	mes "プロンテラ騎士団所属の精鋭メンバー";
	mes "ブルーマーだ！ファファファファ！";
	mes "騎士団員になったばかりだが";
	mes "カッコよくない？";
	mes "どう？";
	next;
	switch(SKILL_DEAD) {
	case 0:
		mes "[ブルーマー]";
		mes "やぁ、ノービスくん！";
		mes "君のように若くて純粋な目を";
		mes "見ると、昔の私を思い出すよ…";
		mes "…";
		mes "あ、まるでオジサンみたいな";
		mes "ことを言っているな…";
		next;
		mes "[ブルーマー]";
		mes "俺だってまだ若いしな、フフ…";
		mes "努力すればきっと希望の職につける。";
		mes "まぁ…素質もあるに越した事無いが。";
		mes "そこで！ノービス時代に習得した";
		mes "なんとも便利なスキルについて";
		mes "教えてやろうか！";
		next;
		mes "[ブルーマー]";
		mes "…知りたいことは知りたいが";
		mes "疑いを隠せないといった表情だな…";
		mes "今のお前にはかなり助け";
		mes "になるかもしれない技術だぞ。";
		next;
		mes "[ブルーマー]";
		mes "お、興味が沸いてきたな？";
		mes "よし、俺の必殺技について話して、";
		mes "伝授してやろう。人呼んで";
		mes "^3355FF「死んだふり」^000000!!!";
		mes "いや、やってみれば分かる。";
		mes "本当の死人に見えるから。";
		next;
		mes "[ブルーマー]";
		mes "^3355FF「死んだふり」^000000は";
		mes "ノービスにぴったり。";
		mes "これえも極度の忍耐力を要する";
		mes "いたってマジメなスキルなんだ。";
		mes "死んだふりをしたままでいることは";
		mes "実は意外に難しいんだよ。";
		next;
		mes "[ブルーマー]";
		mes "強力なモンスターに出くわした時、";
		mes "かなわないと思って死んだふりを";
		mes "したとしよう。しかしそいつの知能が";
		mes "高かった場合、お前をくすぐったり";
		mes "して確かめてきたら今のお前は";
		mes "耐えられるか？";
		next;
		mes "[ブルーマー]";
		mes "このように想定しうる様々な場合に";
		mes "完壁に死んだように見せるってのは";
		mes "忍耐の極限を極めた者でなくては";
		mes "できないだろう…";
		mes "それが死んだふりの必須条件だ。";
		next;
		mes "[ブルーマー]";
		mes "まぁ説明はこの辺にするか。";
		mes "お前の目からも学ぶ姿勢が";
		mes "感じられるしな。";
		mes "それでは訓練コースを";
		mes "進んで行け。ああそうそう、";
		mes "このカプセルを飲んでな。";
		next;
		mes "[ブルーマー]";
		mes "10分以内にプロンテラ城内";
		mes "東塔2 階の看護将校様に";
		mes "お会いして^3355FF「初心者の名札」^000000を";
		mes "いただいて来るように。";
		next;
		mes "^3355FF- ゴクン (薬をのんだ) -^000000";
		next;
		mes "[ブルーマー]";
		mes "ふふ、お前が今飲んだ薬は";
		mes "呼吸を小さくする作用をする。";
		mes "「あー！だまされた！」という";
		mes "顔だな。大丈夫だ、身体には";
		mes "特に問題はない。忍耐力を";
		mes "育てるには長距走が良いかと";
		next;
		mes "[ブルーマー]";
		mes "思ってな。これも修行のうちだ。";
		mes "それでは、出発しろ。";
		mes "遅かったらもう一度行く羽目に";
		mes "なるぞ、ほらほら、急いで";
		mes "行ってこーい！";
		set SKILL_DEAD,1;
		close;
	case 1:
		mes "[ブルーマー]";
		mes "ほらほら時間が無くなるぞ〜";
		mes "お前のん気だな。";
		mes "スキルを覚える気が無いのか？";
		mes "走って行けー！";
		next;
		mes "[ブルーマー]";
		mes "プロンテラ看護将校様は";
		mes "プロンテラ城内東側塔2 階に";
		mes "いらっしゃるぞ！";
		close;
	case 2:
		if(@novice_tag == 0) {
			mes "[ブルーマー]";
			mes "ほらほらー";
			mes "初心者の名札はどこにいった？";
			mes "どこかで落としてきたのか？";
			mes "しょうがないな…";
			mes "気の毒ではあるが";
			mes "始めからやり直し！";
			next;
			mes "[ブルーマー]";
			mes "お前少し甘く見てるようだが";
			mes "プロンテラ騎士団員の席は";
			mes "そう甘く得られたものじゃ無い。";
			mes "やるときはやるんだぞ！";
			mes "では、気を取り直して";
			mes "始めからいってみようかー！";
			set SKILL_DEAD,1;
			close;
		}
		mes "[ブルーマー]";
		mes "よーし！持って来たな！";
		mes "うむ、テストは合格だ！";
		mes "そのカプセルのの効力に負けず";
		mes "看護将校様の怒鳴りつけにも";
		mes "堪えるなんて…もうそのまま";
		mes "死んだふりすれば死人に見えるな。";
		next;
		mes "[ブルーマー]";
		mes "うむ。これからかなわない敵に";
		mes "出くわしたら、迅速な判断のもと";
		mes "死んだふりをしろ。";
		mes "ふふ、それではいつかまた会おう！";
		skill 143,1,0;
		set SKILL_DEAD,0;
		set @novice_tag,0;
		close;
	}
}

//-----------------------------------------------
//==== 看護将校
//-----------------------------------------------
prt_castle.gat,175,146,4	script	看護将校	50,{
	mes "[ドレッド]";
	mes "…";
	mes "……";
	mes "気が散る！";
	mes "私の目の前でうろつかずに";
	mes "どこか行きたまえ！";
	next;
	if(getskilllv(142) < 1) {	//応急手当クエスト
		switch(SKILL_AID) {
		case 0:
			mes "[ドレッド]";
			mes "こちらはプロンテラ近衛団看護課";
			mes "である！生死の境に立つ患者を";
			mes "対象に応急手当をする訓練と";
			mes "実践を行っているため、";
			mes "常に全隊員が緊張に包まれている。";
			next;
			mes "[ドレッド]";
			mes "看護将校である私とて同じく";
			mes "常に神経が張り詰めているから";
			mes "用事済んだら速やかに帰るように。";
			close;
		case 1:
			mes "[ドレッド]";
			mes "ほう…";
			mes "包帯が欲しいようだが";
			mes "それがどのように作られるか";
			mes "知っているのかね？";
			mes "モロク地方のピラミットダンジョン";
			mes "そこに包帯をぐるぐると巻いた";
			next;
			mes "[ドレッド]";
			mes "ミイラのようなモンスターがいる。";
			mes "そやつの腐った包帯をしっかりと";
			mes "消毒する。";
			mes "どうだ？信じられるか？";
			mes "ふふ…そいつの包帯には";
			mes "毒気が含まれており、";
			next;
			mes "[ドレッド]";
			mes "なぜかその成分が同じような";
			mes "毒に作用する。";
			mes "毒は毒を以って制す…";
			mes "日常でもよく聞く話だろう。";
			mes "…普段の私だったら";
			mes "もう少しかまってやれるのだが";
			next;
			mes "[ドレッド]";
			mes "今は少し疲れている…";
			mes "これだ、持って行くように。";
			mes "いつか治療が上手くなったら";
			mes "私たち看護チームの助けをしてくれ。";
			mes "包帯欲しさにミイラみたいな奴に";
			mes "手を出して死ぬなよ。";
			next;
			mes "^3355FF- 消毒した包帯を受け取った -^000000";
			set SKILL_AID,2;
			close;
		case 2:
			mes "[ドレッド]";
			mes "ほら、用が済んだら";
			mes "さっさと帰れ。";
			mes "私は少し休みたい。";
			mes "…";
			mes "ウトウト…";
			close;
		}
	}
	if(getskilllv(143) < 1) {	//死んだふりクエスト
		switch(SKILL_DEAD) {
		case 0:
			break;
		case 1:
			if(@novice_tag) {
				mes "[ドレッド]";
				mes "ほら！";
				mes "まだ私に何か用か？";
				mes "初心者の名札は一つで十分だろ！";
				mes "年中初心者でいるつもりか";
				mes "この怠け者めが!!";
				close;
			}
			mes "[ドレッド]";
			mes "こらーー！";
			mes "(せっかくうたた寝していたのに…)";
			mes "何故神聖なプロンテラ城内を";
			mes "走り回っておるか！";
			mes "(ガミガミ)";
			mes "一体何用で走り回っている！";
			next;
			mes "[ドレッド]";
			mes "…";
			mes "…何？";
			mes "初心者の名札が欲しくて";
			mes "来ただと？";
			mes "- サッ -";
			next;
			mes "^3355FF- 初心者の名札を受け取った -^000000";
			set @novice_tag,1;
			set SKILL_DEAD,2;
			close;
		case 2:
			mes "[ドレッド]";
			mes "ほら、何を呆けておる！";
			mes "走れ走れ！";
			mes "初心者の時代はひたすら進め！";
			close;
		}
	}
	mes "[ドレッド]";
	mes "なんだ…？あっちへ行け！";
	mes "私は騒々しいのは";
	mes "嫌いだ!!";
	close;
}

