//===== rAthena Script =======================================
//= Archer Skill Quest
//===== By: ==================================================
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: ===================================== 
//= Auriga Project
//===== Description: ========================================= 
//= Quest for skills: Arrow Crafting, Arrow Repel
//===== Additional Comments: ================================= 
//============================================================

//追加スキル Part 3 - 弓手
//---------------------------
//==== 矢作成 ===
//---------------------------
morocc.gat,121,109,5	script	ギョルナリ	88,{
	if(getskilllv(147)) {
		mes "[ギョルナリ]";
		mes "あ、故郷の方ですね！";
		mes "嬉しい！";
		mes "お元気ですか？";
		mes "あ！その矢…";
		mes "ご自分で作ったのですね！";
		next;
		mes "[ギョルナリ]";
		mes "ふふ…";
		mes "器用になりましたね。";
		mes "とにかく顔を見せてくれて";
		mes "嬉しいです。";
		mes "またぜひ立ち寄ってくださいね。";
		mes "それではさようなら〜";
		close;
	}
	if(getbaseclass(Class) != CLASS_AC) {
		mes "[ギョルナリ]";
		mes "弓手はめずらしいですか？";
		mes "道中で見かけると思いますが。";
		mes "私はただの弓手ではなく、";
		mes "技術を教えている者です。";
		close;
	}
	if(Job == Job_Archer && JobLevel<30) {
		mes "[ギョルナリ]";
		mes "うん？何かお話ですか？";
		mes "んー私からは特にお話は…";
		mes "…";
		mes "実力に差がありすぎると";
		mes "話について来れないでしょう。";
		close;
	}
	mes "[ギョルナリ]";
	mes "あ、";
	mes "あなたは…";
	next;
	if(countitem(907)<20 || countitem(7033)<7 || countitem(906)<41 || countitem(1019)<13 || countitem(501)<1) {
		mes "[ギョルナリ]";
		mes "フェイヨンの弓手さん！";
		mes "嬉しいです！こんなところで";
		mes "仲間に会うなんて！";
		mes "一人でモロクに来て";
		mes "知らない人ばかりで…";
		mes "とても寂しかったんです…";
		next;
		if(select("大変でしたね…元気出してください","ずっと苦労してください")==2) {
		 	mes "[ギョルナリ]";
			mes (Sex)? "…兄さん…": "…お嬢さん…";
			mes "夜道には気をつけてください…";
			close;
		}
		mes "[ギョルナリ]";
		mes "う…ありがとうございます…";
		mes "あなたもこの見知らぬ地では";
		mes "苦労が多いことでしょう。";
		mes "矢を調達するのも大変でしょう。";
		mes "私はそれを自分で作れるように";
		mes "お教えしている者です。";
		next;
		menu "本当ですか!?",-;
		mes "[ギョルナリ]";
		mes "ええ、いくつかのアイテムを";
		mes "利用して矢を作ることができます。";
		mes "険しい世の中を一人で生きていく";
		mes "のに重要な技術です。";
		mes "もし必要ならお教えしますよ。";
		next;
		menu "お願いします",-;
		mes "[ギョルナリ]";
		mes "いいでしょう。ただし、";
		mes "私のわがままも聞いてくれますか？";
		mes "うーん…と";
		mes "私の欲しい物を集めてきて";
		mes "くださったらスキルをお教えします。";
		next;
		mes "[ギョルナリ]";
		mes "私は故郷を離れてとても寂しいです。";
		mes "故郷の品物でその寂しさを慰めて";
		mes "ほしいのです…";
		mes "フェイヨン森の^3355FF古木に流れる^000000";
		mes "^3355FF露20個^000000、フェイヨンの商店に";
		mes "売っている^3355FF赤ポーション1本^000000";
		next;
		mes "[ギョルナリ]";
		mes "そして、フェイヨン付近の森に住む";
		mes "ウィローから出た^3355FF木屑13個^000000、";
		mes "ワームテールの^3355FF鱗の茎41個^000000";
		mes "^3355FF毒きのこの胞子7個^000000";
		mes "を持って来てくだされば";
		next;
		mes "[ギョルナリ]";
		mes "その時スキルを教えてさしあげます。";
		mes "それではお待ちしています。";
		mes "故郷の品々を…";
		close;
	}
	mes "[ギョルナリ]";
	mes "持ってきましたか！";
	mes "ありがとうございます。";
	mes "それでは約束通り、";
	mes "スキルをお教えします。";
	next;
	delitem 907,20;
	delitem 7033,7;
	delitem 906,41;
	delitem 1019,13;
	delitem 501,1;
	skill 147,1,0;
	mes "[ギョルナリ]";
	mes "もう矢の心配が少なく";
	mes "なりましたね。";
	mes "そうそう、フェイヨンで「カイゴ」";
	mes "という奴に会いましたか？";
	mes "気をつけてください。";
	mes "奴はとても凶暴な奴です…";
	next;
	mes "[ギョルナリ]";
	mes "フェイヨンに行った時は";
	mes "カイゴには気をつけてくださいね。";
	mes "覚えておいてください。";
	mes "それではお元気で、";
	mes "良い贈り物をありがとう〜";
	close;
}

//---------------------------
//==== チャージアロー ===
//---------------------------
payon.gat,103,63,4	script	カイゴ	88,3,3,{
	//クローンスキルを考慮して職業判定を先にする
	if(getbaseclass(Class) != CLASS_AC) {
		mes "[???]";
		mes "寂しい一人身の俺を見て";
		mes "何かおもしろいのか…？";
		close;
	}
	if(getskilllv(148)) {
		mes "[カイゴ]";
		mes "よう、また会ったな。";
		mes "どうしてか俺は見知った奴だと";
		mes "言い方がぶっきらぼうになるんだ。";
		mes "まぁ、とにかく嬉しいぜ。";
		close;
	}
	if(Job == Job_Archer && JobLevel<35) {
		mes "[カイゴ]";
		mes "ふむ、お前弓手だな？";
		mes "もう少しがんばれば";
		mes "良いものを得られるかもしれんぞ。";
		mes "がんばれ！";
		close;
	}
	mes "[カイゴ]";
	mes "クソ…傷がよく治らんな…";
	mes "こんな体たらくとは俺としたことが";
	mes "油断したな…ちぇっ…";
	mes "それにしても、この俺に";
	mes "こんな傷をつけるとは…";
	next;
	mes "[カイゴ]";
	mes "ギョルナリめ…どうしてくれようか。";
	mes "う〜む……あ？";
	mes "お前、いつからそこに居た？";
	mes "なかなかできる奴だな。";
	mes "お前ほどの使い手ならば";
	mes "「チャージアロー」も使えるだろう。";
	next;
	if(select("それは何ですか？","早く教えて")==1) {
		mes "[カイゴ]";
		mes "…冗談だろ？";
		mes "いや…お前ほどの使い手が";
		mes "チャージアローも知らないとは";
		mes "珍しい奴だと思ってな。。";
		next;
		mes "(カイゴはしばし考える)";
		next;
		mes "[カイゴ]";
		mes "ふむ、よし、俺が直々に";
		mes "教えてやるか。";
		next;
		mes "[カイゴ]";
		mes "チャージアローは、攻撃時に";
		mes "敵を少し後ろに押し出す。";
		mes "狙いを定めるのに多少時間を";
		mes "要すが、魔法等と違い、攻撃を";
		mes "受けても妨害されない。";
		next;
		mes "[カイゴ]";
		mes "近距離戦が苦手な弓手にとっては";
		mes "ありがたい技術だろう。";
		mes "お前もチャージアローを覚える気が";
		mes "あったら、また俺のところに来い。";
		mes "ある準備をしてな。";
		next;
		mes "[カイゴ]";
		mes "まず弓を改良しなければならないから";
		mes "使わないクロスボウを一つ用意だ。";
		mes "そして^3355FF触手10個^000000と^3355FF鳥のくちばし10個^000000";
		mes "^3355FFサルのしっぽ3個^000000…良い弾力のな。";
		mes "そして^3355FFエメラルド2個^000000。最後に…";
		mes "私が好きな^3355FFバナナジュース36本^000000！";
		next;
		mes "[カイゴ]";
		mes "以上";
		mes "これだけ揃えて来たら";
		mes "チャージアローを教えてやるよ。";
		mes "じゃ、いってらっしゃい。";
		mes "(バナナジュースを持ってこなかったら";
		mes "殴るしかないな…)";
		close;
	}
	if(countitem(721)<2 || countitem(942)<3 || countitem(962)<10 || countitem(925)<10 || countitem(532)<36) {
		mes "[カイゴ]";
		mes "ん…？";
		mes "足りない物があるな…";
		mes "もう一度言うが、^3355FFエメラルド 2^000000";
		mes "^3355FFサルのしっぽ 3^000000、^3355FF触手 10^000000";
		mes "^3355FF鳥のくちばし 10^000000、最後に";
		mes "^3355FFバナナジュース 36本^000000！";
		next;
		mes "[カイゴ]";
		mes "しっかり揃えてまた来いよ！";
		close;
	}
	mes "[カイゴ]";
	mes "よし！完璧だ！";
	mes "俺がみんなのアーチャースキル";
	mes "チャージアローを伝授してやる！";
	next;
	delitem 721,2;
	delitem 942,3;
	delitem 962,10;
	delitem 925,10;
	delitem 532,36;
	skill 148,1,0;
	mes "[カイゴ]";
	mes "ん、思ったより出来が良い。";
	mes "弓を改良する必要もない！";
	mes "これはまた持って行きな。";
	mes "そしてもうどこへなり行って";
	mes "新しい技を披露して威張れ！";
	mes "ふはは！";
	close;
OnTouch:
	mes "[???]";
	mes "くう……";
	close;
}

