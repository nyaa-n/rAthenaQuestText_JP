//===== rAthena Script ======================================= 
//= Dancer Skills Quests
//===== By: ================================================== 
//===== Current Version: ===================================== 
//= 1.0
//===== Compatible With: ===================================== 
//= Auriga Project
//===== Description: ========================================= 
//= Quests for skills: Charming Wink
//===== Additional Comments: ================================= 
//============================================================

//-----------------------------------------------
//==== 魅惑のウィンク - DC_WINKCHARM
//	■ SKILL_WINKCHARM   -> 0〜8
//-----------------------------------------------
comodo.gat,204,172,5	script	サネル	724,{
	if(getskilllv(1011)) {
		emotion 32;
		mes "[サネル]";
		mes "うん？　何か用？";
		mes "あー、わかった。";
		mes "あんたも私に惚れたな!?";
		mes "フフ。美しさは罪よね。";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "…………";
		next;
		mes "-もう何も驚かない-";
		close;
	}
	if(getbaseclass(Class,2) != CLASS_DC) {
		if(Sex) {
			mes "[サネル]";
			mes "ちょっとお兄さん。";
			mes "こっち向いて♪";
			mes "（チュッ）";
			emotion 30;
			misceffect 364,"";
			next;
			mes "-なんだか胸がどきどきする-";
			close;
		}
		else {
			mes "[サネル]";
			mes "あら、そんなに";
			mes "お姉さんがきれいなのかしら〜？";
			mes "鼻の下が伸びてるわよ。ほら♪";
			next;
			mes "（……殴ってしまおうか……？）";
			close;
		}
	}
	if(JobLevel < 40) {
		mes "[サネル]";
		mes "あら、まだまだ青二才ね。";
		mes "もうちょっと大きくなれば";
		mes "一人前のレディーになることが";
		mes "大変だと分かるようになるわ。";
		mes "職業レベルが40位にならないと";
		mes "分からないかしら？";
		close;
	}
	switch(SKILL_WINKCHARM) {
	case 0:
		mes "[サネル]";
		mes "惜しい……惜しいわね。";
		mes "その美貌や実力を";
		mes "そのままにしてしまうのは";
		mes "ちょっと、もったいなくない？";
		next;
		mes "[サネル]";
		mes "存在するだけでも";
		mes "ありがたいのが女性ってもんよ。";
		next;
		mes "[サネル]";
		mes "でも、常に自分の魅力のため、";
		mes "努力を怠っちゃいけないという事は";
		mes "ちゃんと分かってる？";
		mes "どう？";
		mes "あなたは自分の魅力に自信があるの？";
		next;
		if(select("えぇ、勿論よ！","……ごめんなさい…………")==1) {
			mes "[サネル]";
			mes "まだまだよ！　このばか!!";
			next;
			mes "（パチン！）";
			misceffect 1,"";
			percentheal -5,0;
			close;
		}
		mes "[サネル]";
		mes "自分の過ちを知って、";
		mes "それを直そうと努力するなら";
		mes "過ちではないわ。";
		mes "でも……";
		next;
		mes "[サネル]";
		mes "生まれながらの長所を傷つけているなら";
		mes "それは罪よ！";
		mes "分かった!?";
		next;
		mes "（パチン！　パチン！）";
		misceffect 1,"";
		percentheal -10,0;
		next;
		mes "[サネル]";
		mes "多くの男たちのハートを奪える";
		mes "可愛いレディーに生まれているのに、";
		mes "その程度の魅力で";
		mes "ダンサーと言える!?";
		next;
		mes "[サネル]";
		mes "私がこれから、";
		mes "人々の心を溶かして、";
		mes "悪魔さえ正気でいられないようにして、";
		mes "天使も抜け殻のようにしてしまう";
		mes "すごい魅力を与えてあげる。";
		next;
		mes "[サネル]";
		mes "あなたを完璧なレディーにしてあげる。";
		mes "どう？　私に任せてみない？";
		next;
		if(select("嫌よ!!","お願いします、先生！")==1) {
			mes "[サネル]";
			mes "まだわかってないわね!?";
			mes "今の私の魅力はあんたより";
			mes "断然上なのよ！";
			mes "先輩の凄さを教えてやる！";
			next;
			mes "（パチン！　パチン！　パチン！）";
			misceffect 1,"";
			percentheal -10,0;
			next;
			mes "-この人からはたして";
			mes "女性の魅力を学ぶことができるのか？-";
			mes "-とても疑問だが、";
			mes "怖くて言い返す事ができない！-";
			close;
		}
		mes "[サネル]";
		mes "女性を美しくするのは";
		mes "自分への自信よ。";
		mes "マインドコントロールと";
		mes "イメージトレーニングが";
		mes "美しいレディーになるための";
		mes "必須要素よ。";
		next;
		mes "[サネル]";
		mes "それじゃあ、ちょっと";
		mes "^FF0000鏡^000000を持ってきてくれる？";
		next;
		mes "[短気なサネル]";
		mes "何よ、その目つきは？";
		mes "そんな目で突っ立ってないで";
		mes "さっさと行ってこれないの!?";
		next;
		misceffect 0;
		mes "（パチン）";
		mes "-彼女は鞭で地面を";
		mes "強く叩いた-";
		set SKILL_WINKCHARM,1;
		close;
	case 1:
		if(countitem(747) < 1) {
			misceffect 107;
			mes "[息苦しいサネル]";
			mes "^FF0000鏡^000000よ、^FF0000鏡^000000!!";
			mes "今すぐ持って来いっていってるでしょ！";
			close;
		}
		mes "[サネル]";
		mes "さあ、鏡を持ってきたなら";
		mes "よーく見てごらんなさい。";
		mes "そして何度も言うの。";
		mes "私はきれい！";
		mes "私は魅力的よ！";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "わ……私はきれい……";
		mes "……私は魅力的よ…………";
		next;
		mes "[自己陶酔したサネル]";
		mes "もっと大きな声で！";
		mes "私は美しい！";
		mes "私は世界で一番美しい！";
		next;
		misceffect 99,"";
		mes "[自己陶酔した" +strcharinfo(0)+ "]";
		mes "わ、私はとても美しい!!";
		next;
		mes "-なんと！-";
		mes "-鏡の中に映っている";
		mes "自分の姿が、本当に";
		mes "世界で一番きれいに見えた-";
		delitem 747,1;
		set SKILL_WINKCHARM,2;
		close;
	case 2:
		mes "[サネル]";
		mes "そう、まず最初に";
		mes "自分の美しさを知るの。";
		mes "自分が美しいと思えば";
		mes "誰でも自信が持てるって";
		mes "決まっているのよ。";
		next;
		mes "[サネル]";
		mes "でも、いくら自分が可愛いと思えても";
		mes "他人にそう見えなかったら";
		mes "まったくの無意味なのよ。";
		next;
		mes "[サネル]";
		mes "自分の魅力を";
		mes "他人に認めてもらいたいなら";
		mes "その魅力を効果的に";
		mes "表現する方法を学ぶ必要があるわ。";
		next;
		mes "[サネル]";
		mes "そのためには、";
		mes "その分野に精通した人に";
		mes "会ってみるほうが早いわね。";
		next;
		mes "[サネル]";
		mes "プロンテラ聖堂の近くに";
		mes "私の姉さんがいるわ。";
		mes "あなたに色々教えてくれるはずよ。";
		next;
		mes "[サネル]";
		mes "それでも、";
		mes "私ぐらいに美しくなるのは";
		mes "モンスターでも大変よ。";
		mes "ホーホホホホホホ。";
		next;
		emotion 7,"";
		mes "[我慢する" +strcharinfo(0)+ "]";
		mes "…………!";
		next;
		mes "-耐える君の心に光あれ-";
		set SKILL_WINKCHARM,3;
		close;
	default:
		mes "[サネル]";
		mes "ちょっと！";
		mes "テキパキ動く事ができないの？";
		mes "そんなふうにポカーンとしてたら";
		mes "腰のぜい肉が増えるわよ！";
		next;
		emotion 27;
		mes "[サネル]";
		mes "プロンテラ聖堂の近くよ！";
		mes "わかったらさっさと行く！";
		mes "ＧＯ、ＧＯ、ＧＯ！";
		close;
	case 7:
		mes "[サネル]";
		mes "どう？";
		mes "何か聞けた？";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "それが……話の途中で……";
		mes "アエルさんが……アルコールで……";
		next;
		mes "[サネル]";
		mes "やっぱりね。";
		mes "そんなことだろうと思ったわ。";
		mes "大体何があったか分かるわ。";
		next;
		mes "[サネル]";
		mes "要するにあなたは";
		mes "何をすればいいか分かったけど、";
		mes "どうやってすればいいのか、";
		mes "方法が分からないということね？";
		next;
		mes "[サネル]";
		mes "……";
		next;
		mes "[サネル]";
		mes "てい！";
		misceffect 1,"";
		percentheal 5,0;
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "????";
		mes "何するんですか！";
		next;
		mes "[サネル]";
		mes "なんでもかんでも";
		mes "一から教えてもらえると思ってんの！";
		mes "それぐらい自分で考えなさい！";
		next;
		mes "[親切なサネル]";
		mes "…………と言いたい所だけど。";
		mes "私は親切なサネルちゃん♪だし、";
		mes "大体のコツは教えてあげるわ。";
		next;
		mes "-親切な人の条件に";
		mes "人を殴らない、ということは";
		mes "入っていないのだろうか？-";
		set SKILL_WINKCHARM,8;
		close;
	case 8:
		mes "[親切なサネルちゃん]";
		mes "コツを言うわよ。";
		mes "1秒間ゆっくり目を閉じて";
		mes "2秒後に目を開けるの。";
		mes "こんな風に。";
		mes "うっふん♪";
		emotion 21;
		next;
		if(select("うっふん","うふん♪","うっふん♪")!=3) {
			mes "[サネル]";
			mes "きちんとできないの!?";
			next;
			mes "（パチン！）";
			misceffect 1,"";
			percentheal -5,0;
			close;
		}
		mes "[" +strcharinfo(0)+ "]";
		mes "うっふん♪";
		emotion 21,"";
		misceffect 99,"";
		next;
		mes "-何だか自信がでてきた！-";
		next;
		mes "[サネル]";
		mes "よくできたわね。";
		mes "その要領を忘れないようにね。";
		mes "チュッ♪";
		emotion 30;
		next;
		mes "[サネル]";
		mes "あと、一つ注意する点があるとすると、";
		mes "誰でもこのウィンクであなたの虜に";
		mes "なると思ったら大間違いよ。";
		mes "世の中にはあなたでは手にあまるような";
		mes "強い人がいっぱいいるのよ。";
		next;
		emotion 18;
		mes "[サネル]";
		mes "もし相手があなたより強いと、";
		mes "このウィンクは通じないわ。";
		mes "まずは足が遅い相手に";
		mes "使ってみたほうがいいわよ♪";
		next;
		mes "[サネル]";
		mes "それだけ肝に銘じておけば";
		mes "これ以上教える事はないわ。";
		mes "もう正々堂々と一人前のレディーとして";
		mes "人間も、天使も、悪魔も";
		mes "皆、虜にすることができるわ♪";
		next;
		mes "[サネル]";
		mes "それじゃ、もうお別れよ。";
		mes "今度また会う時は";
		mes "今よりもっと";
		mes "輝いていなさい。";
		next;
		misceffect 253,"";
		mes "^4D4DFF-魅惑のウィンクスキルを覚えた！-^000000";
		set SKILL_WINKCHARM,0;
		skill 1011,1,0;
		close;
	}
}

prontera.gat,183,333,3	script	アエル	79,{
	if(getskilllv(1011)) {
		emotion 7;
		mes "[頭が痛いアエル]";
		mes "……ウウウ……二日酔いが……";
		next;
		mes "[アエル]";
		mes "あら、あなたは？";
		mes "へぇ……";
		mes "今なら立派なレディーに見えるわ。";
		next;
		mes "[アエル]";
		mes "これからは、自分が";
		mes "一人前のレディーだと自覚なさい。";
		mes "レディーは存在するだけで";
		mes "皆の感謝を受ける存在なのよ。";
		next;
		mes "[アエル]";
		mes "思いっきり甘えて、駄々をこねて、";
		mes "誰だろうと、何でもいいから";
		mes "お願いしてみなさい。";
		next;
		mes "[アエル]";
		mes "誰でもひざまずかせれる私のように";
		mes "素敵なレディーになれるよう";
		mes "祈ってあげるわ。";
		mes "大変だけどね、ウフフ〜♪";
		next;
		mes "-本当にどこまでも似ている姉妹だ-";
		close;
	}
	switch(SKILL_WINKCHARM) {
	default:
		mes "[アエル]";
		mes "オークダンジョン、";
		mes "グラストヘイム、";
		mes "ミョルニール廃鉱、";
		mes "無料でワープポータル";
		mes "しますよ〜！";
		next;
		mes "[??????]";
		mes "また勝手にワープポータルを";
		mes "使ってるんですか！";
		mes "アエルさん！　どこですか！";
		mes "聖堂に戻ってきなさい！";
		next;
		emotion 23;
		mes "[アエル]";
		mes "やば、神父様だ！";
		mes "ここにいることは秘密よ!!";
		close;
	case 3:
		mes "[アエル]";
		mes "オークダンジョン、";
		mes "グラストヘイム、";
		mes "ミョルニール廃鉱、";
		mes "無料でワープポータル";
		mes "しますよ〜";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "あの……";
		next;
		mes "[アエル]";
		mes "どこに送りましょうか？";
		mes "ワープポータルは三箇所ですよ〜";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "サネルのお姉さんですか？";
		next;
		mes "[アエル]";
		mes "ん？　ちぇっ。";
		mes "何だ、お客さんじゃないんだ。";
		mes "うちの馬鹿な妹が紹介したってとこ？";
		next;
		mes "-彼女はとてもがっかりした";
		mes "ようだが、あわててぎこちなく";
		mes "ほほ笑んだ-";
		set SKILL_WINKCHARM,4;
		close;
	case 4:
		mes "[アエル]";
		mes "あの子の紹介なら";
		mes "用件は想像つくわ。";
		mes "すぐに教えてあげれるけど、";
		mes "一つ条件があるわ。";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "条件!?";
		next;
		mes "[アエル]";
		mes "今までずっと客寄せしてたから";
		mes "喉が渇いたのよ。";
		mes "何か飲み物でも持って来てくれない？";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "え……飲み物ですか？";
		mes "ポーションとか!?";
		next;
		mes "[アエル]";
		mes "あんた本当に気がきかないね。";
		mes "飲み物と言ったら";
		mes "^FF0000アルコール^000000位は基本よ！";
		mes "きほん！";
		next;
		mes "[アエル]";
		mes "そしてもうちょっとセンスがあるなら";
		mes "おつまみも持って来てくれるわよね。";
		mes "お酒のおつまみには^FF0000果物^000000が";
		mes "ピッタリよね〜";
		next;
		mes "[アエル]";
		mes "^FF0000リンゴが1個^000000、";
		mes "^FF0000バナナが3本^000000、";
		mes "^FF0000クッキーは5個^000000、";
		mes "この三つを^FF0000お皿^000000に";
		mes "盛ってきてちょうだい。";
		next;
		mes "[アエル]";
		mes "ほら、すぐに行きなさいよ!!";
		mes "あ、聖堂の方に";
		mes "気づかれないようにしてね〜！";
		set SKILL_WINKCHARM,5;
		close;
	case 5:
		if(countitem(970) < 1 || countitem(512) < 1 || countitem(513) < 3 || countitem(538) < 5 || countitem(736) < 1) {
			mes "[アエル]";
			mes "…………";
			mes "サネルが言ったとおり";
			mes "本当にトロい子だねぇ。";
			next;
			mes "[アエル]";
			mes "^FF0000アルコールが1本^000000、";
			mes "^FF0000リンゴが1個^000000、";
			mes "^FF0000バナナが3本^000000、";
			mes "^FF0000クッキーは5個^000000、";
			mes "これらを^FF0000お皿^000000に盛ってきて！";
			next;
			mes "[アエル]";
			mes "ほら、さっさとお酒を持って来て！";
			mes "お酒!!";
			mes "アルコールばんざーい!!!!!!!!!!!!";
			close;
		}
		mes "[アエル]";
		mes "キャ。いいわねぇ。";
		mes "喉が渇いた時はやっぱこれよね！";
		next;
		mes "-彼女は瞬く間に";
		mes "それらを全てたいらげた-";
		next;
		mes "[ご機嫌アエル]";
		mes "お腹いっぱ〜い、しあわせ〜！";
		mes "よし！";
		mes "それじゃあ、あんたに";
		mes "私の秘密のスキルを";
		mes "伝授してあげるわ。";
		next;
		mes "[アエル]";
		mes "苦労しないで自分の魅力を";
		mes "相手にアピールすることができるのが";
		mes "何かわかる？";
		mes "それは^FF0000ウィンク^000000よ、^FF0000ウィンク^000000。";
		next;
		mes "[アエル]";
		mes "単純に片方の目をつむる。";
		mes "そんな小さな動作なのに、";
		mes "その効果はまるでスクリーム。";
		mes "ハートに突き刺さる";
		mes "ダブルストレイフィング!!";
		mes "と言っても過言じゃないわよ〜！";
		next;
		mes "-彼女は酔いが回ったのか、";
		mes "よくわからないセリフを";
		mes "交ぜながら話し始めた-";
		next;
		mes "[アエル]";
		mes "目をちょっとつむるだけで、";
		mes "人間でも、悪魔でも、天使さえも";
		mes "あっという間に惚れさせて";
		mes "しまうのよ。";
		next;
		mes "[酔っ払ったアエル]";
		mes "頭の上にハートが浮かんだら成功よ。";
		mes "攻撃をしてきたモンスターも";
		mes "そん時だけは、あんたの虜〜ニハハ。";
		next;
		mes "[酔っ払ったアエル]";
		mes "そうなっても、";
		mes "女王様のように訳もなく殴るのはダメ。";
		mes "それじゃあ五千年の愛情も";
		mes "遠くへ飛んでっちゃうわ〜。";
		next;
		emotion 21;
		mes "[酔っ払ったアエル]";
		mes "よぅし！";
		mes "私のウィンクを見てなさい！";
		mes "こんな感じよ！";
		mes "（うっふん）";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "あ……あれ!?";
		mes "なぜか……";
		mes "なんだかあなたがきれいに見える!!";
		mes "きれいに見えるわ！";
		mes "あなたの事が";
		mes "好きになっちゃったみたい！";
		next;
		emotion 18;
		mes "[酔っ払ったアエル]";
		mes "ウフフン。年季が違うのよ〜！";
		mes "私が本気になっちゃえば";
		mes "バフォメットやオークヒーロー、";
		mes "エンジェリングだって";
		mes "虜にできちゃうんだから〜！";
		mes "アハハハハハンハ〜♪";
		next;
		percentheal -10,0;
		misceffect 1,"";
		emotion 9,"";
		mes "-彼女はあなたの頭をばしばし叩いて";
		mes "気持ち良さそうに笑った-";
		mes "-とても腹が立ったが";
		mes "なんとか堪えれた-";
		delitem 970,1;
		delitem 512,1;
		delitem 513,3;
		delitem 538,5;
		delitem 736,1;
		set SKILL_WINKCHARM,6;
		close;
	case 6:
		mes "[" +strcharinfo(0)+ "]";
		mes "（妹さんとお姉さんのウィンクは、";
		mes "すごい効果だったけど……）";
		mes "私が普通にウィンクしても";
		mes "だめみたい……";
		mes "何かコツでもあるの!?";
		next;
		mes "[寝ているアエル]";
		mes "zZzz……Zzzz……";
		mes "…………むにゃ……";
		mes "私はうつくしー……";
		mes "……zzzZzz……";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "ちょっと!!";
		mes "寝ないで！　起きて！";
		mes "コツを教えて!!";
		next;
		mes "[寝起きのアエル]";
		mes "起こすな!!!!";
		mes "あっちへ行け!!";
		set SKILL_WINKCHARM,7;
		close2;
		warp "prontera.gat",156,272;
		end;
	case 7:
	case 8:
		mes "[" +strcharinfo(0)+ "]";
		mes "ちょっと、何するのよ!!";
		next;
		mes "[アエル]";
		mes "むにゃむにゃ。";
		mes "無理やり起こすからよ！";
		mes "あとは妹にでも聞きなさい！";
		close2;
		warp "prontera.gat",156,272;
		end;
	}
}
