//===== rAthena Script ======================================= 
//= Wizard Skills Quests
//===== By: ================================================== 
//===== Current Version: ===================================== 
//= 1.0
//===== Compatible With: ===================================== 
//= Auriga Project
//===== Description: ========================================= 
//= Quests for skills: Sight Blaster
//===== Additional Comments: ================================= 
//============================================================

//-----------------------------------------------
//==== サイトブラスター - WZ_SIGHTBLASTER
//	■ SKILL_SIGHTBLASTER   -> 0〜3
//-----------------------------------------------
gef_tower.gat,115,36,4	script	ニャオ	876,{
	if(getskilllv(1006)) {
		mes "[ニャオ]";
		mes "Zzz……Zzz……";
		mes "ウーン……ムニャ……";
		mes "カトリーヌ……今にみてなさい……";
		close;
	}
	switch(SKILL_SIGHTBLASTER) {
	case 0:
		mes "[ニャオ]";
		mes "ウーン……眠い……";
		mes "ムニャムニャ……";
		next;
		mes "[ニャオ]";
		mes "眠いのに……";
		mes "私に何か用でもあるの？";
		next;
		if(select("ここになぜ猫が？","私も眠い……")==1) {
			mes "[ニャオ]";
			mes "うん……猫……？";
			mes "そうか……今は猫だったわね……";
			mes "アハハハハハッ!!";
			next;
			mes "[ニャオ]";
			mes "カトリーヌめ……ちょっと属性原石を";
			mes "盗んで使った程度で私を猫に";
			mes "変えてこき使うなんて！";
			mes "あー……考えただけでも腹が立つわ！";
			next;
			mes "[ニャオ]";
			mes "私が人間に戻ったらみてなさい！";
			mes "私を猫にしたカトリーヌはもちろん、";
			mes "猫と呼んだ者達も皆猫にしてやるわ！";
			next;
			mes "[ニャオ]";
			mes "お前もよ！";
			close;
		}
		mes "[ニャオ]";
		mes "眠いのになぜ私に声を掛けるの？";
		mes "私もゆっくり眠ろうとしてるんだから、";
		mes "邪魔しないで家へ帰って寝なさい！";
		if(getbaseclass(Class,2) != CLASS_WZ)
			close;
		next;
		mes "[ニャオ]";
		mes "…………";
		next;
		mes "[ニャオ]";
		mes "……眠くてさっきは分からなかったけど";
		mes "よく見たらお前はウィザードのようね。";
		mes "ちょうどいいわ。";
		mes "ウィザード達の為に新たに編み出された";
		mes "スキルがあるけど……";
		mes "覚えてみる？";
		next;
		if(select("覚えるのも面倒……寝ます","どんなスキルですか？")==1) {
			mes "[ニャオ]";
			mes "せっかく教えてあげようとしたのに……";
			mes "面倒なら息もしなければいいじゃない？";
			mes "何の為に生きてるの？";
			mes "私が教えてあげたくてこんな事";
			mes "言ってると思ってる？";
			next;
			mes "[ニャオ]";
			mes "ふぅ……";
			mes "カトリーヌもお前も……";
			mes "皆大嫌い！";
			close;
		}
		mes "[ニャオ]";
		mes "私の編み出したスキルじゃないけどね。";
		mes "カトリーヌが新たに編み出したスキルを";
		mes "ウィザード達に教えなければ、";
		mes "私を永遠に猫にしておくと脅してくる";
		mes "からね……はぁ……。";
		next;
		mes "[ニャオ]";
		mes "そういえばカトリーヌの横にいる";
		mes "マリアも、前々から人間になるとは";
		mes "言ってるけどまだ犬のままのようね。";
		mes "カトリーヌの言葉を信用しても本当に";
		mes "良いのかしらね……。";
		next;
		mes "[ニャオ]";
		mes "余計な話が長くなったわね。";
		mes "とにかくどんなスキルか説明するから、";
		mes "とりあえずしっかり聞いて。";
		next;
		mes "[ニャオ]";
		mes "ウィザード達の強力な魔法は遠距離の";
		mes "敵を攻撃する事ができるけど、";
		mes "急に敵が近くに出現して接近されると、";
		mes "他の職業に比べて接近戦に弱い";
		mes "ウィザードは厳しいわ。";
		next;
		mes "[ニャオ]";
		mes "これから私が教えるスキルはそれを";
		mes "補うために編み出されたスキルよ。";
		next;
		mes "[ニャオ]";
		mes "スキルを使用したら、";
		mes "一定時間魔法の炎が自分の";
		mes "周辺を回りだすの。";
		next;
		mes "[ニャオ]";
		mes "その間急に敵が出現して襲ってきても、";
		mes "近寄られた瞬間に周囲の魔法の";
		mes "炎が敵を少し弾き飛ばして、";
		mes "ダメージを与えるの。";
		next;
		mes "[ニャオ]";
		mes "どう？　とっさの事態からの脱出に";
		mes "とても便利だと思わない？";
		next;
		menu "そうですね。ところで弾き飛ばすとは？",-;
		mes "[ニャオ]";
		mes "うーん……言葉通り自身に近づいた敵を";
		mes "一定距離弾き飛ばすという事ね。";
		mes "これは言うのは簡単でもやってみると";
		mes "かなり難しい技術よ。";
		mes "弾き飛ばす技術の原理がわからなければ";
		mes "スキルを覚えても使えないでしょう。";
		next;
		mes "[ニャオ]";
		mes "お前はこんな種類のスキルを使った事が";
		mes "ないから原理は分からないでしょう。";
		mes "でも、他の職業達……特にナイトが";
		mes "その技術をよく使うから、彼等に聞けば";
		mes "教えてくれるかもしれないわね……。";
		next;
		mes "[ニャオ]";
		mes "プロンテラ11時方向にある";
		mes "騎士団に行ってみなさい。";
		mes "そこで弾き飛ばす技術の原理を学んで、";
		mes "それからもう一度私に会いに来なさい。";
		set SKILL_SIGHTBLASTER,1;
		close;
	case 1:
		mes "[ニャオ]";
		mes "何をしてるの？";
		mes "プロンテラの騎士団で弾き飛ばす技術の";
		mes "原理を学んで来なさいと言ったでしょ！";
		close;
	case 2:
		mes "[ニャオ]";
		mes "弾き飛ばす技術の原理を";
		mes "学んで来たみたいね。";
		mes "じゃあ本格的にスキルを教えて";
		mes "あげる……とでも言うと思った？";
		next;
		mes "[ニャオ]";
		mes "授業料を払いなさい！授業料を！";
		mes "ボランティアじゃないわよ！";
		next;
		mes "[ニャオ]";
		mes "私がカトリーヌの属性原石をたくさん";
		mes "盗んで使い尽くしたから、";
		mes "属性原石がここにはないのよ。";
		mes "だから属性原石を持ってきてくれれば、";
		mes "授業料を出した事にしてあげる。";
		next;
		mes "[ニャオ]";
		mes "^FF0000クリスタルブルー10個";
		mes "イエローライブ10個";
		mes "レッドブラッド10個";
		mes "ウィンドオブヴェルデュール10個^000000";
		next;
		mes "[ニャオ]";
		mes "これだけ集めて持ってきなさい。";
		mes "あまり難しくはないでしょう？";
		mes "しっかり数を揃えて持ってきてね。";
		mes "私は一眠りしているから……ふぁ……。";
		set SKILL_SIGHTBLASTER,3;
		close;
	case 3:
		if(countitem(991) < 10 || countitem(993) < 10 || countitem(990) < 10 || countitem(992) < 10) {
			mes "[ニャオ]";
			mes "ふぁ……何？　まだ集めてないの？";
			mes "何を持ってくるのか忘れた？";
			mes "じゃあもう一度だけ教えてあげる。";
			next;
			mes "[ニャオ]";
			mes "^FF0000クリスタルブルー10個";
			mes "イエローライブ10個";
			mes "レッドブラッド10個";
			mes "ウィンドオブヴェルデュール10個^000000";
			next;
			mes "[ニャオ]";
			mes "これだけ集めて持ってきなさい。";
			mes "全て揃うまでは起こさないで！";
			close;
		}
		mes "[ニャオ]";
		mes "ふぁ……ううん……誰？";
		mes "うーん……ああ……属性原石は全て";
		mes "揃えてきた？";
		next;
		mes "[ニャオ]";
		mes "背中に担いで来たようね。";
		mes "じゃあ、ちょっと確かめるわ。";
		next;
		mes "[ニャオ]";
		mes "一つ、二つ、三つ、四つ……";
		next;
		mes "[ニャオ]";
		mes "あー……数えるの面倒ね。";
		mes "だいたい合ってるからいいわ。";
		mes "じゃあスキルを教えるわね。";
		mes "スキルの名前は『^FF0000サイトブラスター^000000』";
		mes "なかなかいい名前だと思わない？";
		next;
		mes "[ニャオ]";
		mes "この魔法は火属性だから先に";
		mes "火属性魔法を身に纏わないとだめよ。";
		mes "お前もウィザードだからその程度は";
		mes "難しくないでしょう？";
		mes "一度やってみなさい。";
		next;
		misceffect 183,"";
		mes "-ポポポン!!　ポン!!-";
		next;
		mes "[ニャオ]";
		mes "……………………";
		next;
		mes "[ニャオ]";
		mes "お前本当にウィザードなの？";
		mes "……まぁその部分は練習で";
		mes "身に着ければ良いわね。";
		mes "じゃあ次は…………";
		next;
		mes "-一週間ニャオの厳しい特訓を受けた-";
		mes "-" +strcharinfo(0)+ "はようやく";
		mes "^FF0000サイトブラスター^000000を使えるようになった-";
		misceffect 253,"";
		delitem 991,10;
		delitem 993,10;
		delitem 990,10;
		delitem 992,10;
		set SKILL_SIGHTBLASTER,0;
		skill 1006,1,0;
		next;
		mes "[ニャオ]";
		mes "ふぅ……一週間お疲れ様。";
		mes "お前のように才能のない";
		mes "ウィザードには初めて会ったわ。";
		mes "とにかくスキルはもう教えたから、";
		mes "私が教えるべき事はないわね。";
		next;
		mes "[ニャオ]";
		mes "下手な使い方をして、それを教えた";
		mes "私の名前に泥を塗らないように。";
		mes "苦労して教えたんだから";
		mes "うまく使いなさい。";
		next;
		mes "[ニャオ]";
		mes "一週間もずっと力を使ってたから";
		mes "疲れてしょうがないわ。";
		mes "私はもう寝るわよ！";
		close;
	}
}

prt_in.gat,82,98,3	script	セイモンメイヤス	46,{
	if(SKILL_SIGHTBLASTER != 1) {
		mes "[セイモン]";
		mes "やっぱり戦いは剣でしないとな！";
		close;
	}
	mes "[セイモン]";
	mes "若いウィザードが私達の騎士団に";
	mes "何の用事で来たのかな？";
	next;
	if(select("何でもないです","弾き飛ばす技術の原理を学びに来ました")==1) {
		mes "[セイモン]";
		mes "お前の体格なら騎士になっても";
		mes "良かったのに……やっぱり";
		mes "戦いは剣でしないとな！";
		close;
	}
	mes "[セイモン]";
	mes "弾き飛ばす技術の原理？　ハハハッ！";
	mes "ウィザードがそれを知ってどうする？";
	mes "お前は火の障壁みたいな魔法で";
	mes "敵の接近を防いだ後、上から降らす";
	mes "火の雨でパパパパッ！　てすれば";
	mes "いいんじゃないのか？";
	next;
	menu "自分自身の身を守る方法を知る為です",-;
	mes "[セイモン]";
	mes "ふむ……そういえば君の職業は";
	mes "接近戦に弱い職だから、";
	mes "敵に近寄られたら弾き飛ばす";
	mes "というのは効果的だな。";
	mes "よし！教えてやろう！";
	next;
	mes "[セイモン]";
	mes "打つという事と弾き飛ばすという事は";
	mes "違うものだ。";
	mes "打つという事は早い速度で物体に";
	mes "打撃を加え、その瞬間の破壊力で";
	mes "敵を倒す。";
	next;
	mes "[セイモン]";
	mes "打撃を加えた後には素早く元の姿勢に";
	mes "戻って、次の攻撃あるいは防御に";
	mes "備えるのが基本だ。";
	next;
	mes "[セイモン]";
	mes "敵を弾き飛ばす攻撃もこれと";
	mes "少し似ているが違う。";
	next;
	mes "[セイモン]";
	mes "弾き飛ばす攻撃も打つ攻撃と同じく、";
	mes "早い速度で物体に打撃を加えて、";
	mes "瞬間の破壊力を上げる。";
	next;
	mes "[セイモン]";
	mes "しかし打つ攻撃と違い、その後に";
	mes "一定した力をずっと加える事で、";
	mes "敵を後ろに弾き飛ばすようにする事だ。";
	next;
	mes "[セイモン]";
	mes "もちろん弾き飛ばす時の力は打つ瞬間";
	mes "半減されるから、この時一定の力を";
	mes "加え続けるのがポイントだ。";
	next;
	mes "[セイモン]";
	mes "そしてもう一つ、重要な事は弾き飛ばす";
	mes "瞬間どこの部分に打ち込んでいるかだ。";
	mes "あまり上の方に打ち込むと、";
	mes "敵の弾き飛ばされる距離が減って、";
	mes "逆に下の方に打ち込むと、";
	mes "敵は前に倒れる。";
	next;
	mes "[セイモン]";
	mes "一番良い部分はやっぱり中心だ！";
	mes "敵の中心部分に攻撃を加えた後、";
	mes "一定の力を加え続ければ敵は後ろに";
	mes "弾き飛ばされるという事だ。";
	next;
	mes "[セイモン]";
	mes "これが弾き飛ばす技術の原理だ。";
	mes "この程度で十分な説明だろう。";
	next;
	mes "[セイモン]";
	mes "もっと聞きたい事はあるか？";
	mes "弾き飛ばす技術の原理なら";
	mes "この程度で十分だ。";
	mes "それをどう応用するかはお前次第だ。";
	set SKILL_SIGHTBLASTER,2;
	close;
}