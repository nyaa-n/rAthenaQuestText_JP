//===== rAthena Script ======================================= 
//= Sage Skills Quests
//===== By: ================================================== 
//===== Current Version: ===================================== 
//= 1.0
//===== Compatible With: ===================================== 
//= Auriga Project
//===== Description: ========================================= 
//= Quests for skills: Create Converter, Elemental Change
//===== Additional Comments: ================================= 
//============================================================
//-----------------------------------------------
//==== クリエイトコンバーター - SA_CREATECON
//==== エレメンタルチェンジ - SA_ELEMENT
//	■ SKILL_ELEMENT   -> 0〜6
//-----------------------------------------------
yuno_in03.gat,176,24,3	script	ミシュナ	755,{
	if(getskilllv(1018) || getskilllv(1017) || getskilllv(1019) || getskilllv(1008)) {
		mes "[ミシュナ]";
		mes "新しい知識をまだ習っていないセージが";
		mes "身近にいらっしゃったら、私を必ず";
		mes "訪問するようにと言い伝えて下さい。";
		mes "この前お教えした知識を皆様に";
		mes "覚えていただきたいのです。";
		close;
	}
	if(getbaseclass(Class,2) != CLASS_SA) {
		mes "[ミシュナ]";
		mes "こんにちは。";
		mes "私はシュバイチェル魔法アカデミー";
		mes "所属のセージ、ミシュナと申します。";
		mes "あなたはもしかしてセージでは";
		mes "ないでしょうか？";
		next;
		mes "[ミシュナ]";
		mes "………………";
		next;
		mes "[ミシュナ]";
		mes "あっ……違うみたいですね。";
		mes "申し訳ありません。";
		mes "私、ここで働き出したばかりの新人で、";
		mes "緊張して失敗してしまいます……。";
		next;
		mes "[ミシュナ]";
		mes "もしあなたの身近にセージの方が";
		mes "いらっしゃったら、";
		mes "シュバイチェル魔法アカデミー所属の";
		mes "セージ、ミシュナが捜していたと";
		mes "伝えていただけますか？";
		next;
		mes "[ミシュナ]";
		mes "本当に失礼しました。";
		mes "それでは良い一日を！";
		close;
	}
	switch(SKILL_ELEMENT) {
	case 0:
		mes "[ミシュナ]";
		mes "こんにちは。";
		mes "私はシュバイチェル魔法アカデミー";
		mes "所属のセージ、ミシュナと申します。";
		mes "どのようなご用件で";
		mes "いらっしゃいましたか？";
		next;
		menu "新しい知識を得る為に来ました",-;
		mes "[ミシュナ]";
		mes "ああ、セージ" +strcharinfo(0)+ "様";
		mes "ですね？　私もあなたを探してました。";
		mes "過去に失われた大賢者バルムントの";
		mes "知識が、少しですがアカデミーで";
		mes "復元されたので、セージの方々に";
		mes "この新しい知識を伝えているのです。";
		next;
		mes "[ミシュナ]";
		mes "新たに発見された知識は";
		mes "火、地、風、水の4大属性に";
		mes "係わるスキルです。";
		next;
		mes "[ミシュナ]";
		mes "モンスターの属性を4大属性に変換する";
		mes "^FF0000エレメンタルチェンジ^000000とそれを使う為に";
		mes "必要なエレメンタルコンバーターを";
		mes "製造する^FF0000クリエイトコンバーター^000000が";
		mes "それにあたります。";
		next;
		mes "[ミシュナ]";
		mes "過去に傭兵の反乱事件が起こった時、";
		mes "大賢者バルムントの邸宅が燃えてしまい";
		mes "多くの知識が焼失してしまいました。";
		mes "これだけでも復元されたのは、";
		mes "本当に幸いだと思います。";
		next;
		mes "[ミシュナ]";
		mes "では、新しい知識を学ぶ準備は";
		mes "できましたか？";
		next;
		if(select("今は忙しいのでまたにします","すぐ新しい知識を教えて下さい")==1){
			mes "[ミシュナ]";
			mes "あ……どうやら今は他の事で";
			mes "忙しいようですね。";
			mes "準備ができたらいつでも私に";
			mes "声をかけていただければ結構です。";
			mes "それでは良い一日を！";
			close;
		}
		mes "[ミシュナ]";
		mes "まず最初に学ばなくてはならないのは、";
		mes "^FF0000エレメンタルチェンジ^000000を使う為に";
		mes "必要な、エレメンタルコンバーターを";
		mes "製造する^FF0000クリエイトコンバーター^000000です。";
		mes "コンバーターがスキル使用に必須なので";
		mes "これを先に学ぶ必要があります。";
		next;
		mes "[ミシュナ]";
		mes "エレメンタルコンバーターを";
		mes "製造する為には、以下の材料が";
		mes "必要です。";
		next;
		mes "[ミシュナ]";
		mes "^FF0000サソリの尻尾10個";
		mes "硬い角7個";
		mes "華麗な蟲の皮12個";
		mes "カタシムリの皮10個";
		mes "白紙のスクロール4個^000000";
		next;
		mes "[ミシュナ]";
		mes "この材料を集めて持って来て下さい。";
		mes "コンバーター製造に必要な材料なので、";
		mes "必ず全て集めて私を訪ねて下さい。";
		mes "それらを使って、コンバーターの土台を";
		mes "作成しますので、集めてきたらスキルを";
		mes "お教えしますね。";
		set SKILL_ELEMENT,1;
		close;
	case 1:
		if(countitem(904) < 10 || countitem(947) < 7 || countitem(1013) < 12 || countitem(946) < 10 || countitem(7433) < 4){
			mes "[ミシュナ]";
			mes "まだ材料が集まっていないようですね。";
			mes "もう一度必要な材料をお教えします。";
			next;
			mes "[ミシュナ]";
			mes "^FF0000サソリの尻尾10個";
			mes "硬い角7個";
			mes "華麗な蟲の皮12個";
			mes "カタシムリの皮10個";
			mes "白紙のスクロール4個^000000";
			next;
			mes "[ミシュナ]";
			mes "この材料を集めて私に持ってくれば、";
			mes "^FF0000クリエイトコンバーター^000000を";
			mes "お教えします。";
			close;
		}
		mes "[ミシュナ]";
		mes "全部集めてきましたね。";
		mes "ご苦労様です。";
		mes "それではこれからスキルを";
		mes "お教えします。";
		next;
		mes "[ミシュナ]";
		mes "さっきも言いましたが、";
		mes "エレメンタルコンバーターは、";
		mes "今持って来た材料で";
		mes "土台を作成します。";
		next;
		mes "[ミシュナ]";
		mes "材料を揃えて方法が分かれば誰でも";
		mes "作れるか？　というとそうではなく、";
		mes "ここで必要なのがセージ固有の";
		mes "属性付与スキルなのです。";
		next;
		mes "[ミシュナ]";
		mes "ファイアーエレメンタルコンバーターは";
		mes "^FF0000フレイムランチャー^000000が必要で、";
		mes "アースエレメンタルコンバーターは";
		mes "^FF0000サイズミックウェポン^000000が必要です。";
		next;
		mes "[ミシュナ]";
		mes "同じように、";
		mes "ウィンドエレメンタルコンバーターは";
		mes "^FF0000ライトニングローダー^000000が必要で、";
		mes "ウォーターエレメンタルコンバーターは";
		mes "^FF0000フロストウェポン^000000が必要です。";
		next;
		mes "[ミシュナ]";
		mes "そして、スキルを使用しても確実に";
		mes "製造されるわけではありません。";
		mes "製造に失敗して材料だけ失われる";
		mes "可能性もあります。";
		mes "その成功率は属性付与スキルのレベルと";
		mes "あなたの能力で決まります。";
		next;
		mes "[ミシュナ]";
		mes "さあ、これが大賢者バルムントが";
		mes "残した知識です。";
		mes "これを読んでスキルを習得して下さい。";
		next;
		mes "-大賢者バルムントが残したという-";
		mes "-継ぎ紙の写本を読んで、-";
		mes "-^FF0000クリエイトコンバーター^000000を覚えた-";
		misceffect 253,"";
		delitem 904,10;
		delitem 947,7;
		delitem 1013,12;
		delitem 946,10;
		delitem 7433,4;
		set SKILL_ELEMENT,2;
		skill 1007,1,0;
		next;
		mes "[ミシュナ]";
		mes "本当に凄い速度で覚えましたね！";
		mes "とても賢いと噂のセージ";
		mes strcharinfo(0)+ "様の";
		mes "実力は噂通りですね！";
		next;
		mes "[ミシュナ]";
		mes "残るスキルはコンバーターを使って、";
		mes "モンスターの属性を変える";
		mes "^FF0000エレメンタルチェンジ^000000です。";
		mes "少しの間、私は食事をしてきますので、";
		mes "スキルを覚える準備ができたら、";
		mes "また後で来て下さい。";
		next;
		mes "[ミシュナ]";
		mes "スキル習得お疲れ様でした！";
		close;
	case 2:
		mes "[ミシュナ]";
		mes "今度は^FF0000エレメンタルチェンジ^000000を";
		mes "身に付ける番ですね。";
		next;
		mes "[ミシュナ]";
		mes "自分に該当の属性付与スキルさえあれば";
		mes "製造が可能だった";
		mes "^FF0000クリエイトコンバーター^000000とは違い、";
		mes "^FF0000エレメンタルチェンジ^000000は";
		mes "一つの属性だけ習得する事ができます。";
		next;
		mes "[ミシュナ]";
		mes "もちろん該当の属性付与スキルが";
		mes "なければ覚える事はできないし、";
		mes "一回その属性に決めたら他の属性に";
		mes "変える事もできないから、";
		mes "その点に注意して下さい。";
		next;
		mes "[ミシュナ]";
		mes "どの属性の^FF0000エレメンタルチェンジ^000000を";
		mes "覚えますか？";
		mes "もう一度注意しておきますが、";
		mes "一回属性を決めると、";
		mes "変更はできません。";
		while(1) {
			next;
			setarray '@skillname$,"ファイアーエレメンタルチェンジ","アースエレメンタルチェンジ","ウィンドエレメンタルチェンジ","ウォーターエレメンタルチェンジ";
			setarray '@needskillname$,"フレイムランチャー","サイズミックウェポン","ライトニングローダー","フロストウェポン";
			setarray '@needskillid,280,283,282,281;
			setarray '@needitemid,990,993,992,991;
			set @menu,select("ファイアーエレメンタルチェンジ","アースエレメンタルチェンジ","ウィンドエレメンタルチェンジ","ウォーターエレメンタルチェンジ");
			if(getskilllv('@needskillid[@menu-1]) == 0) {
				mes "[ミシュナ]";
				mes "属性付与スキル^FF0000" +'@needskillname$[@menu-1]+ "^000000を";
				mes "習得してないと、";
				mes "^FF0000" +'@skillname$[@menu-1]+ "^000000は";
				mes "習得できません。";
				mes "他の属性を選択してください。";
				continue;
			}
			mes "[ミシュナ]";
			mes "^FF0000" +'@skillname$[@menu-1]+ "^000000を";
			mes "選択するんですね。";
			mes "該当の属性の能力を覚える為には、";
			mes "次の材料が必要です。";
			next;
			mes "[ミシュナ]";
			mes "^FF0000" +getitemname('@needitemid[@menu-1])+ "20個";
			mes "フェイヨン水溶液1個";
			mes "モロク水溶液1個^000000";
			next;
			mes "[ミシュナ]";
			mes "この材料を集めて、";
			mes "私までお持ちいただければ";
			mes "^FF0000" +'@skillname$[@menu-1]+ "^000000を";
			mes "お教えします。";
			mes "では、お待ちしていますね。";
			set SKILL_ELEMENT,SKILL_ELEMENT + @menu;
			close;
		}
	case 3:
		set '@skillname$,"ファイアーエレメンタルチェンジ";
		set '@skillid,1018;
		set '@itemid,990;
		break;
	case 4:
		set '@skillname$,"アースエレメンタルチェンジ";
		set '@skillid,1017;
		set '@itemid,993;
		break;
	case 5:
		set '@skillname$,"ウィンドエレメンタルチェンジ";
		set '@skillid,1019;
		set '@itemid,992;
		break;
	case 6:
		set '@skillname$,"ウォーターエレメンタルチェンジ";
		set '@skillid,1008;
		set '@itemid,991;
		break;
	}
	//スキル習得
	if(countitem('@itemid) < 20 || countitem(1089) < 1 || countitem(1088) < 1) {
		mes "[ミシュナ]";
		mes "まだ材料が集まってないようですね。";
		mes "もう一度お教えしますね。";
		next;
		mes "[ミシュナ]";
		mes "^FF0000" +getitemname('@itemid)+ "20個";
		mes "フェイヨン水溶液1個";
		mes "モロク水溶液1個^000000";
		next;
		mes "[ミシュナ]";
		mes "この材料を集めて、";
		mes "私までお持ちいただければ";
		mes "^FF0000" +'@skillname$+ "^000000を";
		mes "お教えします。";
		mes "では、お待ちしていますね。";
		close;
	}
	mes "[ミシュナ]";
	mes "材料を全て集めてきましたね！";
	mes "それではお約束通り";
	mes "^FF0000" +'@skillname$+ "^000000を";
	mes "お教えします。";
	next;
	mes "[ミシュナ]";
	mes "^FF0000エレメンタルチェンジ^000000は";
	mes "エレメンタルコンバーターとスキルを";
	mes "使用する者の属性付与スキルを使って、";
	mes "ターゲットモンスターの属性を永久に";
	mes "変更するスキルです。";
	next;
	mes "[ミシュナ]";
	mes "^FF0000クリエイトコンバーター^000000と同じく、";
	mes "使用者の能力によって成功率が";
	mes "変化する事に注意して下さい。";
	next;
	mes "[ミシュナ]";
	mes "さあ……今から";
	mes "セージ" +strcharinfo(0)+ "様に、";
	mes "^FF0000" +'@skillname$+ "^000000を";
	mes "覚えさせる呪文を唱えます。";
	mes "少しの間身体を動かさないで下さい。";
	next;
	mes "-ミシュナが呪文を唱えると自分の-";
	mes "-周辺に神秘的で青い光が漂い始めた-";
	next;
	mes "[ミシュナ]";
	mes "ホレテチュチュパレラ!!";
	misceffect 33,"";
	next;
	mes "-何か変な呪文だが-";
	mes "-気にしないでおこう-";
	next;
	mes "[ミシュナ]";
	mes "ホロリイィッヤアッ!!";
	misceffect 70,"";
	delitem '@itemid,20;
	delitem 1089,1;
	delitem 1088,1;
	set SKILL_ELEMENT,0;
	skill '@skillid,1,0;
	next;
	mes "[ミシュナ]";
	mes "これで" +strcharinfo(0)+ "様は";
	mes '@skillname$+ "を";
	mes "習得したはずです。";
	mes "これから有効に活用して下さい！";
	mes "それではお元気で！";
	close;
}

yuno_in03.gat,176,22,3	shop	Scroll商人	67,7433:-1
