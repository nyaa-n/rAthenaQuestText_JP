//===== rAthena Script =======================================
//= Swordsman Skills Quests
//===== By: ==================================================
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= Auriga Project
//===== Description: =========================================
//= Quests for skills: Fatal Blow, Mobile HP Recovery,
//  Auto-Berserk
//===== Additional Comments: =================================
//============================================================

// 追加スキル Part 1 - 剣士
//-----------------------------------------------
//==== 移動時HP回復 - MOVINGRECOVERY
//-----------------------------------------------
izlude_in.gat,175,130,2	script	騎士デトマソ	98,{
	if(getskilllv(144)) {
		mes "[デトマソ]";
		mes "よう、";
		mes "久しぶりだな！";
		mes "前より血色がよくなったようだな。";
		mes "ハハハハ";
		mes "気をつけて行け！また寄りな。";
		close;
	}
	if(getbaseclass(Class) != CLASS_SM) {
		mes "[デトマソ]";
		mes "私の名はデトマソカルロス。";
		mes "プロンテラ第3騎士団所属の騎士だ。";
		mes "ここのところはある任務において";
		mes "活躍中なのである。";
		mes "ゴホン！フッフッフ……";
		close;
	}
	if(Job == Job_Swordman && JobLevel<35) {
		mes "[デトマソ]";
		mes "うん…君も剣を持つ者か…";
		mes "私は、デトマスカルロスという。";
		mes "よく覚えておけよ！";
		mes "プロンテラ第3騎士団の騎士";
		mes "デトマソカルロス！";
		next;
		mes "[デトマソ]";
		mes "もう少し成長したら、また";
		mes "訪ねて来なさい。";
		mes "それでは良い一日を！";
		close;
	}
	if(countitem(713)<200 || countitem(1058)<1) {
		mes "[デトマソ]";
		mes "おい、傷だらけじゃないか！";
		mes "こんな傷だらけになるような";
		mes "熱い戦闘をしてきたのか…";
		mes "やはり剣士の宿命だな。";
		next;
		mes "[デトマソ]";
		mes "そんな剣士のために";
		if(Sex==0)
			mes "良い技術があるぞ、凛々しいお嬢さん！";
		else
			mes "良い技術があるよ、兄弟！";
		mes "人呼んで「移動時HP回復」！";
		mes "これは移動をしながら";
		mes "体力を回復することができる";
		mes "なんとも素晴らしい技術なのだ！";
		next;
		mes "[デトマソ]";
		mes "まだまだ発展途上の技術だから";
		mes "大きな回復能力は期待できないが";
		mes "少しでも君の戦闘に";
		mes "プラスになることは間違いない。";
		mes "どうだ、身につけるか？";
		next;
		if(select("素晴らしい技術！覚えます！","いらないよ")==2) {
			mes "[デトマソ]";
			mes "ガクッ";
			close;
		}
		mes "[デトマソ]";
		mes "よし、それではこのスキルを";
		mes "習得するための条件を教えよう。";
		mes "まず、^3355FF職業レベル35以上^000000が必要だ。";
		mes "そして、^3355FF「空きビン」200個^000000を";
		mes "持ってこい。なぜかわかるか…？";
		mes "それが戦って苦労した証だからだ。";
		next;
		mes "[デトマソ]";
		mes "ん…君が着ている鎧…";
		mes "これがまた勇士の証だな。";
		mes "鎧は…標準的なものなら";
		mes "アーマーがお勧めだぞ。";
		mes "むしろ着て来い。あと最後に";
		mes "^3355FF「蛾の羽」を一つ^000000持ってきてくれ。";
		next;
		menu "え、なんでそんな物が必要なの？",-;
		mes "[デトマソ]";
		mes "いやぁ…なんだ…";
		mes "うちの甥の宿題で";
		mes "昆虫採集がな…ハハハ";
		mes "いや、もちろん私が面倒を見るのが";
		mes "一番だが、仕事でここに居なくては…";
		next;
		mes "[デトマソ]";
		mes "歩き回ることもできずに、同じ場所で";
		mes "働く私が可哀想だと思わないか？";
		mes "ぜひたのむ…しくしく…";
		mes "というわけで、";
		mes "よろしく頼んだ！";
		close;
	}
	mes "[デトマソ]";
	mes "まぁこっちに来い。";
	mes "私はデトマソカルロス。";
	mes "剣を持った若いの、";
	mes "「移動時HP回復」を";
	mes "覚える準備は整ったかね？";
	next;
	if(select("いつでも大丈夫","まだ準備不足です")==2) {
		mes "[デトマソ]";
		mes "そうか";
		mes "それじゃ準備を整えてから";
		mes "また呼んでくれ。";
		close;
	}
	mes "[デトマソ]";
	mes "どれ……………";
	next;
	mes "[デトマソ]";
	mes "よし！それでは今からお前に";
	mes "移動時HP回復スキルを";
	mes "教えてやるぞ。";
	next;
	delitem 713,200;
	delitem 1058,1;
	skill 144,1,0;
	mes "[デトマソ]";
	mes "どうだ、気に入ったか？";
	mes "ん〜実際試して見るように。";
	mes "あまり無理はするなよ。";
	mes "ん？アーマーは必要じゃない。";
	mes "紛らわしかったかスマン…";
	mes "ハハハそれじゃ元気でな！";
	close;
}

//-----------------------------------------------
//==== 急所攻撃 - FATALBLOW(Bashを覚えて無くてもOKらしい)
//-----------------------------------------------
prt_in.gat,75,88,5	script	レオ=フォン=フリッシュ	85,{
	if(getskilllv(145)) {
		mes "[レオ]";
		mes "やぁ、";
		mes "誰かと思ったら";
		mes "前に会ったな。";
		mes "嬉しいねぇ…元気かい？";
		mes "気をつけてな。";
		close;
	}
	if((getbaseclass(Class) != CLASS_SM) || (Job == Job_Swordman && JobLevel<25)) {
		mes "[レオ]";
		mes "ん？君には用が無いところだ。";
		mes "私は一人前の剣士以外には";
		mes "特に言うことが無い。";
		mes "より良き世界でまた会おう。";
		close;
	}
	if(countitem(1752)<10 || countitem(1751)<10 || countitem(532)<1 || countitem(962)<30 || countitem(526)<5) {
		mes "[レオ]";
		mes "ほう、若くてなかなかの剣士だな。";
		next;
		mes "[レオ]";
		mes "その腕からして、バッシュをよく";
		mes "使っているだろう？";
		next;
		menu "なぜそんなことがわかるの？",-;
		mes "[レオ]";
		if(Sex==0) {
			mes "恥ずかしがることはない。";
			mes "剣を持つ者なら女でもそれくらいの";
			mes "腕をしていて当然だろう。";
		}
		else {
			mes "驚くことはない。";
			mes "剣を持つ者たるもの";
			mes "良い腕を持っていて当然じゃないか。";
		}
		mes "このごろ怠けて修行せず、";
		mes "ろくな腕をしていない奴が増えてね。";
		mes "君のような剣士に会えて嬉しい。";
		next;
		mes "[レオ]";
		mes "そう、良い剣士である君に";
		mes "一つプレゼントをしようじゃないか。";
		next;
		menu "どんなプレゼント？",-,"それは楽しみ",-;
		mes "[レオ]";
		mes "ハハ、聞いて驚け、";
		mes "急所を狙うスキルだ！";
		next;
		mes "[レオ]";
		mes "今回私が開発したスキルだ。";
		mes "バッシュを使う時、君のバッシュ";
		mes "スキルによって少しスタン状態に";
		mes "させることができる。";
		mes "バッシュくらいは身につけているか？";
		next;
		mes "[レオ]";
		mes "私が以前君のような剣士だった頃";
		mes "バッシュをよく使ったんだ。その時、";
		mes "同時にスタンさせることができたら";
		mes "もっと良いんじゃないかと思ってな。";
		mes "一線を退いてからずっと研究を続け、";
		mes "やっとその成果を結んだというわけだ。";
		next;
		mes "[レオ]";
		mes "どうだ、私の努力の結晶！";
		mes "そのスキルを身につけてみるか？";
		next;
		switch (select("ぜひ","結構です","寿司を食べる時の注意点は？")) {
		case 1:
			mes "[レオ]";
			mes "よし、それでは条件を言おう。";
			mes "まず、^3355FFバッシュをレベル6^000000にすること。";
			mes "そして、^3355FF炎の矢^000000・^3355FF銀の矢^000000それぞれ^3355FF10本^000000";
			mes "^3355FFバナナジュース1本^000000・^3355FF触手30個^000000";
			mes "^3355FFローヤルゼリー5個^000000を準備するように。";
			mes "一種の材料みたいなものだ。";
			next;
			mes "[レオ]";
			mes "材料を揃えたらまた来なさい。";
			mes "その時続きを教えてやる。";
			close;
		case 2:
			mes "[レオ]";
			mes "ハハハハハハハハハハ！";
			mes "…";
			mes "…";
			mes "…";
			mes "あっそう…";
			close;
		case 3:
			mes "[寿司王レオン]";
			mes "寿司を食べる時は手で";
			mes "食べるのが通だね。";
			mes "基本だろう。";
			mes "そして、醤油はシャリにではなく";
			mes "ネタにつけるんだ。";
			next;
			mes "[寿司王レオン]";
			mes "それでこそ味が豊かになる。";
			mes "食べる順番は、味の淡白なものから";
			mes "だんだん濃いものにしていき、";
			mes "最後は巻き寿司で仕上げる！";
			mes "うーん、寿司は最高！";
			close;
		}
	}
	mes "[レオ]";
	mes "おお、君は「急所攻撃」を";
	mes "覚える条件を満たして来たね。";
	mes "どうだ？今身につけるか？";
	next;
	switch (select("はい！","嫌です","急所攻撃って？")) {
	case 1:
		mes "[レオ]";
		mes "よろしい。";
		next;
		delitem 1752,10;
		delitem 1751,10;
		delitem 532,1;
		delitem 962,30;
		delitem 526,5;
		skill 145,1,0;
		mes "[レオ]";
		mes "成功だ！";
		mes "これからも思いっきり";
		mes "技を振るえよ！";
		mes "ハハハハハハハハ！";
		close;
	case 2:
		mes "[レオ]";
		mes "私は君が嫌いだ。";
		close;
	case 3:
		mes "[レオ]";
		mes "今回私が開発したスキルだ。";
		mes "バッシュを使う時、君のバッシュ";
		mes "スキルによって少しスタン状態に";
		mes "させることができる。";
		mes "バッシュくらいは身につけているか？";
		next;
		mes "[レオ]";
		mes "どうだ、スタンはかなり効果的な";
		mes "テクニックだと思うが。";
		mes "気に入ったらまたおいで。";
		close;
	}
}

//-----------------------------------------------
//==== オートバーサーク
//-----------------------------------------------
prt_in.gat,94,57,3	script	ファン	85,{
	if(getskilllv(146)) {
		mes "[ファン]";
		mes "うーん久しぶり！";
		mes "うまくいってますか？";
		mes "前よりずっとたくましくなりましたね。";
		mes "皆あなたに大きく期待しています。";
		mes "がんばってくださいね。";
		close;
	}
	if(getbaseclass(Class) != CLASS_SM) {
		mes "[ファン]";
		mes "楽しい旅をしていますか？";
		mes "明るい毎日を送れるようお祈りします。";
		mes "あ、私は親切な騎士、ファンです。";
		mes "あまり気を使わずに…ハハハ";
		close;
	}
	if(Job == Job_Swordman && JobLevel<10) {
		mes "[ファン]";
		mes "なんですか？";
		mes "まだまだひよっこですね。";
		mes "私は忙しいからもう少し修練を";
		mes "積んでから来なさい。";
		close;
	}
	if(Job == Job_Swordman && JobLevel<30) {
		mes "[ファン]";
		mes "若者よ、";
		mes "よく来ましたね。";
		close;
	}
	if(countitem(924)<35 || countitem(958)<10 || countitem(957)<10 || countitem(518)<10) {
		mes "[ファン]";
		mes "あら…見ないうちに傷が";
		mes "たくさん増えましたね…";
		mes "そんな身でまた戦場に飛び込む";
		mes "つもりですか？無理しますね…";
		next;
		mes "[ファン]";
		mes "いくら力があっても限界は";
		mes "あるんですから、";
		mes "あまり無理しないでください…";
		mes "もちろん我が研究所が開発した";
		mes "限界を破るスキルもありますが…";
		next;
		switch (select("え？なんですかそれは!?","そんなもの、本当にあるの？","くおおおおお！")) {
		case 1:
			break;
		case 2:
			mes "[口をとがらせたファン]";
			mes "うーん…信じない人に教えてもね…";
			mes "やっぱり何も知りません〜";
			close;
		case 3:
			mes "[興奮したファン]";
			mes "くおおおお！";
			mes "うおおおお！";
			mes "きえええ！";
			close;
		}
	}
	else {
		mes "[ファン]";
		mes "おお、若い剣士よ！";
		mes "最新スキル^3355FF「オートバーサーク」^000000";
		mes "を授かる用意ができましたね？";
		next;
		if(select("ふふ…はやく伝授してください","それは何でしたっけ…")==1) {
			mes "[ファン]";
			mes "よろしい。それでは…";
			next;
			delitem 924,35;
			delitem 958,10;
			delitem 957,10;
			delitem 518,10;
			skill 146,1,0;
			mes "[ファン]";
			mes "もうあなたはオートバーサークを";
			mes "身につけた剣士になったのです。";
			mes "これから、内に秘めた野獣と共に";
			mes "大きな活躍をしてください！";
			next;
			mes "[ファン]";
			mes "幸運を！";
			next;
			mes "[ファン]";
			mes "………そうそう、うっかり忘れて";
			mes "話しそびれるところでした。";
			mes "いくつか覚えておくことがあります。";
			next;
			mes "[ファン]";
			mes "まず、このスキルは体力が";
			mes "回復すればおさまります。";
			mes "そして特に制限時間は無いが、";
			mes "体力の回復によって消えるか";
			mes "プロボック効果を解除するスキル";
			mes "を使用すれば消えます。";
			next;
			mes "[ファン]";
			mes "そんな特性を頭に入れておかないと";
			mes "戦場で急にスキルの効果が消えて";
			mes "荒てるから気を付けてください。";
			next;
			mes "[ファン]";
			mes "それでは、今度こそさようなら〜";
			close;
		}
	}
	mes "[ファン]";
	mes "オートバーサーク。";
	mes "戦場の花と呼ばれるスキルです。";
	mes "あなたの体力が赤色になった時";
	mes "体内の潜在能力がプロボックを";
	mes "発動させるスキルです。";
	next;
	mes "[ファン]";
	mes "戦場で花火のように暴れる…";
	mes "まさにそんなスキルです。";
	mes "今のあなたならこのスキルを";
	mes "体得することができます。";
	mes "それでは、そのための準備について";
	mes "説明をさし上げます。";
	next;
	mes "[ファン]";
	mes "まず、^3355FF「蝶の鱗粉」35個^000000";
	mes "派手な蝶の魂から抜き出した";
	mes "気をあなたに宿らせ集中力を高める。";
	mes "そして^3355FF「亡者の牙」10個^000000";
	mes "^3355FF「亡者の爪」10個^000000";
	mes "最後に^3355FF「ハチ蜜」10個^000000！";
	next;
	mes "[ファン]";
	mes "全てメモをしましたか？";
	mes "いつも通り、全て整えてから";
	mes "またいらっしゃい。";
	mes "期待しています。";
	close;
}
