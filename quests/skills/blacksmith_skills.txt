//===== rAthena Script ======================================= 
//= Blacksmith Skills Quests
//===== By: ================================================== 
//===== Current Version: ===================================== 
//= 1.0
//===== Compatible With: ===================================== 
//= Auriga Project
//===== Description: ========================================= 
//= Quests for skills: Dubious Salesmanship, Greed
//===== Additional Comments: ================================= 
//============================================================

//-----------------------------------------------
//==== フェイクゼニー - BS_UNFAIRLYTRICK
//	■ SKILL_UNFAIRLYTRICK   -> 0〜6
//-----------------------------------------------
geffen.gat,178,72,3	script	アキ	726,{
	if(getskilllv(1012)) {
		if(getskilllv(1013) < 1) {
			mes "[アキ]";
			mes "ブラックスミスの新しいスキル";
			mes "^FF0000グリード^000000を覚えたいなら";
			mes "すぐそこにいる";
			mes "グッドデーさんを尋ねて下さい。";
			close;
		}
		mes "[アキ]";
		mes "お久しぶりです！";
		mes "また、時間があれば";
		mes "尋ねて来て下さい！";
		mes "私は勤務地が変わるまでは";
		mes "ずっとここにいる予定ですから！";
		close;
	}
	if(getbaseclass(Class,2) != CLASS_BS) {
		mes "[アキ]";
		mes "いい天気ですね！";
		mes "今日みたいな日は本当に";
		mes "製錬日和ですよね！";
		next;
		mes "[アキ]";
		mes "綺麗な水が流れる谷で、";
		mes "良い材料と高温度の火を使って、";
		mes "製錬を思いっきりしたいですねー。";
		close;
	}
	if(JobLevel < 30) {
		mes "[アキ]";
		mes "私はブラックスミスギルドで、";
		mes "大勢のギルドの皆さんにスキルを";
		mes "お教えする為に派遣された『アキ』";
		mes "と言います。";
		next;
		mes "[アキ]";
		mes "申し訳ないのですが、";
		mes "[ "+strcharinfo(0)+" ]様は";
		mes "ジョブレベルが不足していて";
		mes "覚える事はできないのです。";
		close;
	}
	switch(SKILL_UNFAIRLYTRICK) {
	case 0:
		mes "[アキ]";
		mes "こんにちはー！";
		mes "今日は天気がとても良いですね。";
		mes "こんな日には商売もいいけど、";
		mes "狩りをしに出かけるのも";
		mes "悪くないですね。";
		next;
		mes "[アキ]";
		mes "私はブラックスミスの新しいスキル";
		mes "^FF0000フェイクゼニー^000000をお教えする為に";
		mes "ブラックスミスギルドから派遣された、";
		mes "職員のアキと言います。";
		next;
		mes "[アキ]";
		mes "私がお教えする新スキルの";
		mes "^FF0000フェイクゼニーは、";
		mes "既存スキルのメマーナイトの";
		mes "zeny消費量を10パーセントも";
		mes "減少させるパッシブスキルです。^000000";
		next;
		mes "[アキ]";
		mes "私が専門家的な目で見る限り、";
		mes strcharinfo(0)+ "様は";
		mes "このスキルを学べばとても有効に";
		mes "使用できると思いますね。";
		next;
		mes "[アキ]";
		mes "どうですか？";
		mes "これを機会に学んでみませんか？";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "-…………………………-";
		mes "-ブラックスミスギルド社員指針書と-";
		mes "-書いてあるパンフレットを-";
		mes "-見ながら私に読み上げている-";
		next;
		if(select("教えてもらう","断る！")==2) {
			mes "[アキ]";
			mes "ええ！　なぜですか！";
			mes "私が頼りなさそうだからですか!?";
			mes "そうなんですか！？";
			next;
			mes "[アキ]";
			mes "うわぁぁぁん！";
			mes "学んでいって下さいー！";
			mes "本当に熱心に教えますから！";
			mes "是非お願いしますー！";
			next;
			mes "[アキ]";
			mes "ブラックスミスギルドの課長が今日、";
			mes "私に必ず実績あげて来なさいと";
			mes "言ったんですー！";
			mes "うぅ…………";
			mes "私、何回始末書書かないと";
			mes "いけないのかな……";
			next;
			mes "[アキ]";
			mes "でも仕方ないですね……";
			mes "今日もまた怒られちゃうけど……";
			mes "ふぇぇぇぇん！";
			close;
		}
		mes "[" +strcharinfo(0)+ "]";
		mes "教えて下さい。";
		next;
		mes "[アキ]";
		mes "よくぞ言ってくれました！";
		mes "今回ブラックスミスギルドで";
		mes "意欲的に開発したスキルですから！";
		mes "ふふふ。";
		next;
		mes "[アキ]";
		mes "では、一応このスキルの原理を";
		mes "先に説明しますね。";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "-また先ほどのパンフレットを-";
		mes "-広げてそれを読み始めた-";
		next;
		mes "[アキ]";
		mes "^FF0000フェイクゼニー^000000とは";
		mes "メマーナイトに使われるzenyの一部に";
		mes "模造品を使用して、";
		mes "威力はそのままで消費zenyを";
		mes "節約するパッシブスキルです。";
		next;
		mes "[アキ]";
		mes "このスキルを学ぶ為には、";
		mes "模造品を作らなければ";
		mes "ならないので、私が言う材料を";
		mes "きっちり集めて私に持ってきて";
		mes "下さい。";
		next;
		mes "[アキ]";
		mes "スキル習得に必要な物は、";
		mes "鋼鉄1個、石炭5個、鋼鉄の金槌1個";
		mes "そして……うーん……ええと……";
		mes "うーんと……うぅぅん……。";
		next;
		mes "[アキ]";
		mes "あっ！　少々お待ち下さい！";
		mes "私まだ新入りなので……";
		mes "あれ……どこだったっけ……";
		mes "資料に線を引いて来たのに、";
		mes "どこーー!?";
		next;
		mes "[アキ]";
		mes "-ペラペラ……-";
		next;
		mes "[アキ]";
		mes "ああー！あった！";
		mes "ありました、ありましたー！";
		mes "それでは続けさせていただきます。";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "…………………………";
		next;
		mes "[アキ]";
		mes "鋼鉄1個、石炭5個、鋼鉄の金槌1個、";
		mes "模造品を作る為の500zeny。";
		mes "そしてディトリミン1個が必要だと";
		mes "ここに書いていますね。";
		mes "あはは！";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "-…………-";
		mes "-果してこの子は-";
		mes "-スキルを教える事が-";
		mes "-できるんだろうか……-";
		next;
		mes "[アキ]";
		mes "今" +strcharinfo(0)+ "様の顔は";
		mes "私がちゃんと教えれるんだろうか？";
		mes "っていう表情をしませんでしたか!?";
		mes "本当にそう思っていたんですか！";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "はい、そうです。";
		next;
		mes "[アキ]";
		mes "うう……やっぱり……";
		mes "でも、ブラックスミスギルドに入って";
		mes "3ヶ月……アキは負けません!!";
		mes "大丈夫です！心配しないで下さい！";
		mes "大船に乗ったつもりで任せて下さい！";
		mes "あははははは！";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "…………………………";
		next;
		mes "[アキ]";
		mes "一応材料さえ集めてしまえば";
		mes "ちゃんと教える事ができるはずです！";
		next;
		mes "[アキ]";
		mes "さあ！それではさっき私が言った";
		mes "材料を集めてきて下さい！";
		next;
		mes "[アキ]";
		mes "確認の為もう一度言いますが、材料は";
		mes "鋼鉄1個、石炭5個、鋼鉄の金槌1個に";
		mes "500zenyとディトリミン1個が";
		mes "必要です！";
		mes "忘れないで下さいね！";
		next;
		mes "[アキ]";
		mes "さあ！出発出発ー！";
		mes "私はその間に買っておいたパンを";
		mes "食べていますね。";
		mes "もぐもぐ……";
		set SKILL_UNFAIRLYTRICK,1;
		close;
	case 1:
		if(countitem(999) < 1 || countitem(1003) < 5 || countitem(613) < 1 || countitem(971) < 1) {
			mes "[アキ]";
			mes "材料は全て手に入れて来ましたか？";
			mes "うーん……どれどれ……";
			mes "あれ？材料が足りないですねー。";
			next;
			mes "[アキ]";
			mes "もう一度言いますが、材料は";
			mes "鋼鉄1個、石炭5個、鋼鉄の金槌1個に";
			mes "500zenyとディトリミン1個が";
			mes "必要です。";
			mes "忘れないで下さいね！";
			close;
		}
		if(Zeny < 500) {
			mes "[アキ]";
			mes "模造品を作るのに使う500zenyが";
			mes "ないみたいですね。";
			mes "早く持って来て下さいね。";
			close;
		}
		mes "[アキ]";
		mes "どうですか？";
		mes "材料を全て手に入れて来ましたか？";
		mes "一度確認してみましょうか";
		mes "ふむふむ……";
		next;
		mes "[アキ]";
		mes "正確に集めてきましたねー、";
		mes "素晴らしいです！";
		next;
		mes "[アキ]";
		mes "一応これで私が先にスキル材料の";
		mes "テストを一度作って見せますので、";
		mes "少しの間待ってて下さい。";
		delitem 999,1;
		delitem 1003,5;
		delitem 613,1;
		delitem 971,1;
		set Zeny,Zeny-500;
		set SKILL_UNFAIRLYTRICK,2;
		close;
	case 2:
		mes "[アキ]";
		mes "あっ!!";
		mes "もういらっしゃいましたか……";
		mes "………………………………";
		mes "えーと……ううん……";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "……………………";
		next;
		mes "[アキ]";
		mes "実は……材料を預かって、";
		mes "早速作ろうとしたのですが、";
		mes "失敗してしまいました……";
		mes "うわぁぁぁん！　申し訳ありません！";
		mes "どうか、どうかギルドには";
		mes "言わないで下さいー！";
		next;
		mes "[アキ]";
		mes "もう一度チャンスを下さい！";
		mes "今度こそ成功させてみせます！";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "本当に……？";
		next;
		mes "[アキ]";
		mes "お願いします！";
		mes "鋼鉄1個と石炭3個だけまた";
		mes "手に入れて下さい！";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "分かりました。";
		mes "それだけ集めてくればいいんですね？";
		next;
		mes "[アキ]";
		mes "はい！　そうです！";
		mes "あなたは本当に良い人です！";
		mes "ありがとうございます！";
		next;
		mes "[アキ]";
		mes "もう一度持ってくる材料の確認です。";
		mes "鋼鉄1個と石炭3個です！";
		mes "よろしくお願いします！";
		mes "ファイト！　ファイト！";
		set SKILL_UNFAIRLYTRICK,3;
		close;
	case 3:
		if(countitem(999) < 1 || countitem(1003) < 3) {
			mes "[アキ]";
			mes "もう一度持ってくる材料の確認です。";
			mes "鋼鉄1個と石炭3個です！";
			mes "よろしくお願いします！";
			mes "ファイト！　ファイト！";
			close;
		}
		mes "[アキ]";
		mes "わあ！　持って来て下さったんですね！";
		mes "本当にありがとうー！";
		mes "今度は本当にうまくやります！";
		mes "それでは少し待ってて下さい！";
		delitem 999,1;
		delitem 1003,3;
		set SKILL_UNFAIRLYTRICK,4;
		close;
	case 4:
		mes "[アキ]";
		mes "……………………";
		mes "………………";
		mes "…………";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "………………？";
		next;
		mes "[アキ]";
		mes "……………………";
		mes "………………";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "あっ……まさか!!";
		mes "また!?";
		next;
		mes "[アキ]";
		mes "うわぁぁぁん！";
		mes "製造し始めたら鋼鉄の金槌が折れて";
		mes "少ししかできなかったんです！";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "-どうやら新しい鋼鉄の金槌を-";
		mes "-持って来なければならないようだ-";
		mes "-…………どうしてだろうか……-";
		mes "-ものすごく疲れる……-";
		set SKILL_UNFAIRLYTRICK,5;
		close;
	case 5:
		if(countitem(613) < 1) {
			mes "[アキ]";
			mes "鋼鉄の金槌が無くて……";
			mes "うぅぅ……ぐすっ";
			mes "もう一度持ってきてくれれば、";
			mes "本当に今度こそ成功させてみせます！";
			close;
		}
		mes "[アキ]";
		mes "おおー！";
		mes "持ってきてくれたんですね！";
		mes "感動です！";
		next;
		mes "[アキ]";
		mes "もう本当の本当にすぐ";
		mes "お教えしますんで、";
		mes "少し待って下さい！";
		delitem 613,1;
		set SKILL_UNFAIRLYTRICK,6;
		close;
	case 6:
		mes "[アキ]";
		mes "うぅ…………";
		mes "わぁぁぁん！";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "えぇぇぇっ!?";
		mes "まさかまたじゃないでしょうね!?";
		next;
		mes "[アキ]";
		mes "なんちゃって！　完成です！";
		mes "あはははは！";
		mes "あっ、ごめんなさい！";
		mes "怒らないで下さい……";
		mes "一度いたずらをしてみたかったんです。";
		next;
		mes "[アキ]";
		mes "スキルが完成して、";
		mes "私もテストしてみたし、";
		mes "準備できたみたいだから";
		mes "^FF0000フェイクゼニー^000000を、";
		mes "お教えしますね。";
		next;
		if(getskilllv(1013) < 1) {
			mes "[グッドデー]";
			mes "それとこのスキルを覚えた後で";
			mes "私の先輩のグッドデーさんに会えば";
			mes "新スキルの^FF0000グリード^000000を";
			mes "教えてくれると思います。";
			next;
		}
		mes "[アキ]";
		mes "それではすぐお教えします。";
		mes "紙と耳を少し貸して下さい。";
		mes "-こそこそ　ひそひそ-";
		next;
		mes "[アキ]";
		mes "私がお教えした通りに";
		mes "うまく使って下さい。";
		mes "私の失敗のせいで迷惑をかけて";
		mes "本当に申し訳ありませんでした。";
		next;
		mes "[アキ]";
		mes "また、時間があれば";
		mes "尋ねて来て下さい！";
		mes "私は勤務地が変わるまでは";
		mes "ずっとここにいる予定ですから。";
		mes "それではお気をつけて冒険の続きを！";
		mes "それじゃあ、またね！";
		set SKILL_UNFAIRLYTRICK,0;
		skill 1012,1,0;
		close;
	}
}


//-----------------------------------------------
//==== グリード - BS_GREED
//	■ SKILL_GREED   -> 0〜1
//-----------------------------------------------
geffen.gat,172,52,1	script	グッドデー	826,{
	if(getskilllv(1013)) {
		mes "[グッドデー]";
		mes "学んだスキルは良く";
		mes "使っているかな？";
		mes "毎日をあまり欲張りすぎないように、";
		mes "色々な所を眺めながら冒険しなさい。";
		close;
	}
	if(getbaseclass(Class,2) != CLASS_BS) {
		mes "[グッドデー]";
		mes "今日みたいな日はカバンを背負って、";
		mes "険しい山にでも登って面白い物や";
		mes "貴重な物探しに行きたくなるな。";
		mes "お前もブラックスミスになって";
		mes "みたらわかると思うぞ！";
		close;
	}
	switch(SKILL_GREED) {
	case 0:
		mes "[グッドデー]";
		mes "私はブラックスミスギルドの";
		mes "スキル伝授担当を統括している";
		mes "マスターグッドデーという！";
		mes "皆、私をグッドマンと呼ぶよ。";
		next;
		mes "[グッドマン]";
		mes "今度新たに商人協会と一緒に";
		mes "開発したスキルを、商人協会と";
		mes "ブラックスミスギルド合同で、";
		mes "ギルドメンバーに教える";
		mes "イベントをしている。";
		next;
		mes "[グッドマン]";
		mes "それで今回のイベントの為に、";
		mes "教育が十分ではない新入社員";
		mes "まで外に出て仕事をしているよ。";
		mes "おかげさまで私までこんな苦労";
		mes "するはめになっているんだ。";
		next;
		mes "[グッドマン]";
		mes "お前もブラックスミスのようだな。";
		mes "新しく開発されたスキル^FF0000グリード^000000を";
		mes "学んでみないか？";
		mes "学ぶ、学ばないはお前の選択だ、";
		mes "だが私が言うのも何だがかなり";
		mes "良いスキルだぞ。";
		next;
		mes "[グッドマン]";
		mes "お前が学ぶのなら、";
		mes "簡単なスキルの説明と";
		mes "その方法をすぐに教えよう。";
		mes "どうかな？";
		next;
		if(select("そうですね、教えて下さい","後でお願いします")==2) {
			mes "[グッドマン]";
			mes "うーん……そうか。";
			mes "若い奴はいつも忙しそうだね。";
			mes "何事にも時間を大切に使うんだぞ。";
			close;
		}
		mes "[グッドマン]";
		mes "うーん、いい判断だ。";
		mes "今回新たに開発されたスキルの、";
		mes "^FF0000グリード^000000は、スキルを使うと、";
		mes "自分の周囲2歩以内の範囲にある、";
		mes "拾得可能な全ての物を、";
		mes "まとめて拾えるとても便利なスキルだ。";
		next;
		mes "[グッドマン]";
		mes "冒険中フィールドに落ちた物を、";
		mes "手軽に素早く回収する事ができるから、";
		mes "とても便利なスキルだ。";
		next;
		mes "[グッドマン]";
		mes "一応、私からスキルを教える前に、";
		mes "お前がスキルを覚える資格を";
		mes "備えているか、簡単なテスト課題を";
		mes "出すから、良く聞いてくれよ。";
		next;
		mes "[グッドマン]";
		mes "^FF0000物の種類や個数に関係なく、";
		mes "お前の所持重量の余裕が";
		mes "500もない状態にして";
		mes "私に会いに来なさい。^000000";
		next;
		mes "[グッドマン]";
		mes "私の出す課題はこれで全部だ。";
		mes "それじゃあ、早くこなしてきてくれ。";
		set SKILL_GREED,1;
		close;
	case 1:
		if((MaxWeight - Weight) >= 5000) {
			mes "[グッドマン]";
			mes "うーん……だめだなあ。";
			mes "ひと目見ても私が言った課題を満たして";
			mes "ないとわかるな。";
			mes "もう一回課題を教えてあげよう。";
			mes "次はこなしてきてくれよ。";
			next;
			mes "[グッドマン]";
			mes "^FF0000物の種類や個数に関係なく、";
			mes "お前の所持重量の余裕が";
			mes "500もない状態にして";
			mes "私に会いに来なさい。^000000";
			close;
		}
		mes "[グッドマン]";
		mes "ん？　もう行って来たのか？";
		mes "思っていたよりすぐ来たな。";
		mes "どれ、一度カバンを見せてくれ。";
		mes "うーん……………………";
		next;
		mes "[グッドマン]";
		mes "どれどれ……";
		mes "うーん……私が言った課題を";
		mes "きっちりとこなして来てくれたな。";
		mes "よし！　合格だ。";
		next;
		mes "[グッドマン]";
		mes "お前がこの物を集める為に";
		mes "過ごした時間と経験が、";
		mes "単純にこれらを全てお金に変えた時の";
		mes "価値よりも、ずっと大切なものなんだと";
		mes "いう事を教えたかったんだ。";
		next;
		mes "[グッドマン]";
		mes "真のブラックスミスなら、";
		mes "この世に存在するほとんど全ての";
		mes "物を目で見て、万物を見ればこそ";
		mes "立派な製造能力と、それにふさわしい";
		mes "富と名誉をもつことができると";
		mes "私は思っているよ。";
		next;
		mes "[グッドマン]";
		mes "このテストと、私がお前に教える";
		mes "スキルとは直接的な関連はないと";
		mes "思えるが、私がブラックスミスに";
		mes "なりたてのころ、私のお師匠様が";
		mes "私に与えたテストとその教えなんだ。";
		next;
		mes "[グッドマン]";
		mes "最近は手軽にお金と名誉を";
		mes "得ようと思う者が多く、";
		mes "お金で価値を付ける事のできる";
		mes "物と物事のみを見て世の中を";
		mes "生きる人が多くなっていて、";
		mes "それを見るたびに胸が痛む気がするよ。";
		next;
		mes "[グッドマン]";
		mes "他の仲間と多くの人々が必要とする";
		mes "材用や武器を鍛える為に、";
		mes "それに使う材料を求めて、";
		mes "自ら大変な冒険の旅に出る！";
		mes "そんな魅力的で格好良い職業が";
		mes "ブラックスミスだからね。";
		next;
		mes "[グッドマン]";
		mes "今、私がお前に教えるスキルの名前は";
		mes "^FF0000グリード^000000と言うが、";
		mes "これは『貪欲』という意味なんだ。";
		mes "お前がこのスキルを覚えて使うように";
		mes "なったら、他の名前で呼ばれるように";
		mes "なる事を祈っているよ。";
		next;
		mes "[グッドマン]";
		mes "さあ、それじゃあ教えようか！";
		set SKILL_GREED,0;
		skill 1013,1,0;
		close;
	}
}