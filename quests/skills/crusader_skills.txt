//===== rAthena Script ======================================= 
//= Crusader Skills Quests
//===== By: ================================================== 
//===== Current Version: ===================================== 
//= 1.0
//===== Compatible With: ===================================== 
//= Auriga Project
//===== Description: ========================================= 
//= Quests for skills: Shrink
//===== Additional Comments: ================================= 
//============================================================

//-----------------------------------------------
//==== シュリンク - CR_SHRINK
//	■ SKILL_SHRINK   -> 0〜8
//-----------------------------------------------
geffen.gat,110,117,3	script	フォード	752,{
	if(getskilllv(1002)) {
		mes "[フォード]";
		mes "スラッティに薬を渡したようだね。";
		mes "ご苦労様！";
		mes "本当にありがとう。";
		next;
		mes "[フォード]";
		mes "お前みたいな奴が";
		mes "我が騎士団に居ると良いが、";
		mes "本当に残念だな。";
		next;
		mes "[フォード]";
		mes "もし、我が騎士団に";
		mes "入りたければ、いつでも来てくれ。";
		mes "お前ならいつでも歓迎するぞ。";
		mes "それじゃ、またな。";
		close;
	}
	if(getbaseclass(Class,2) != CLASS_CR) {
		mes "[フォード]";
		mes "私は今重要な考え事を";
		mes "していて忙しいから";
		mes "邪魔をしないでくれ。";
		close;
	}
	switch(SKILL_SHRINK) {
	case 0:
		mes "[フォード]";
		mes "ふむ〜　どうしたんだろう。";
		mes "もうこんな時間なのに、";
		mes "まだ来ないなんて……。";
		mes "「^FF0000うん?!^000000」";
		next;
		mes "[フォード]";
		mes "おお〜　来たか!!";
		mes "さて、向こうから送られた";
		mes "「^FF0000手紙^000000」は";
		mes "持ってきたのか？";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "へ？　どういう事でしょう……？";
		mes "おじさんとは初対面ですが……。";
		next;
		mes "[フォード]";
		mes "あ、あれ？";
		mes "ゲフェンの南地域で";
		mes "守備任務についている騎士じゃないか？";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "ちがいます……。";
		next;
		mes "[フォード]";
		mes "ふむ……あってるよな？";
		mes "そうだろ？";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "違いますってば。";
		next;
		mes "[フォード]";
		mes "本当に？";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "はい。";
		mes "ちがいますって。";
		mes "……。";
		next;
		mes "[フォード]";
		mes "……。";
		next;
		mes "[フォード]";
		mes "ああ、失礼！";
		mes (Upper == UPPER_HIGH? "パラディン": "クルセイダー")+ "の姿をしていたので";
		mes "つい私が待っていた";
		mes "人だと思い込んじゃったよ。";
		next;
		mes "[フォード]";
		mes "ふむ〜　こりゃ、まいったな。";
		mes "本当に困る……。";
		mes "時間もないのにさ……。";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "何か問題でも……？";
		next;
		mes "[フォード]";
		mes "私はプロンテラ騎士団で";
		mes "守備隊長を引き受けている";
		mes "「^FF0000フォード・レスリー^000000」だ。";
		next;
		mes "[フォード]";
		mes "我らは周辺の警戒区域でモンスターが";
		mes "暴れることを防ぎ、";
		mes "旅人や街の人々を保護する";
		mes "仕事をしている。";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "そうですか。";
		next;
		mes "[フォード]";
		mes "ところが、最近近くの森で";
		mes "「^FF0000ゴブリン^000000」たちが";
		mes "たくさん現われて、人を襲ってくるので";
		mes "かなり頭を悩ませているんだ。";
		next;
		mes "[フォード]";
		mes "それで我々騎士団は";
		mes "周辺地域を巡察し、一日に";
		mes "2回状況を報告してもらっている。";
		next;
		mes "[フォード]";
		mes "ところが、今日に限って";
		mes "報告が遅れている。";
		mes "何か問題が起こったに違いない……。";
		next;
		mes "[フォード]";
		mes "しかし、私は今ここから";
		mes "動く事ができないから";
		mes "確認しに行く事もできないし、";
		mes "本当に困っているんだ。";
		mes "もう報告が届く時間から";
		mes "大分経っている。";
		next;
		mes "[フォード]";
		mes "そういえば、お前も";
		mes (Upper == UPPER_HIGH? "パラディン": "クルセイダー")+ "のようだが、";
		mes "良ければ私の頼みを一つだけ";
		mes "聞いてくれないかな？";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "頼みって何ですか？";
		next;
		mes "[フォード]";
		mes "先ほど言った通り、私は今ここで";
		mes "人を待ってるんだ。";
		next;
		mes "[フォード]";
		mes "ゲフェンの外周にある警備所で";
		mes "防衛任務をしている";
		mes "スラッティというやつが送った";
		mes "向こうの状況報告書が";
		mes "もう届くはずなのだけど、";
		next;
		mes "[フォード]";
		mes "今日に限ってあまりにも遅いんだ。";
		mes "それで、すまんが向こうに行って";
		mes "状況を見て来てくれないか？";
		next;
		mes "[フォード]";
		mes "私は今席を外す事が";
		mes "できないから……";
		mes "頼む！";
		next;
		if(select("了解","断る")==2) {
			mes "[フォード]";
			mes "ふむ……そうか……。";
			mes "まあ、しようがない。";
			mes "あ、いや、こんな事を";
			mes "関係者ではない者に任せる";
			mes "事自体が間違っていたな。";
			mes "分かった。じゃ、気をつけてな。";
			close;
		}
		mes "[フォード]";
		mes "おお！";
		mes "本当にありがとう。";
		mes "やはりお前も" +(Upper == UPPER_HIGH? "パラディン": "クルセイダー")+ "の";
		mes "矜持を持っているのだな。";
		next;
		mes "[フォード]";
		mes "じゃ、時間がないから";
		mes "ゲフェンの南に位置する警戒区域に";
		mes "行って「^FF0000スラッティ^000000」という";
		mes "者を探してくれ。";
		next;
		mes "[フォード]";
		mes "彼に会ったら、報告書を貰って";
		mes "私へ持って来れば良い。";
		mes "それじゃ、宜しく頼む！";
		set SKILL_SHRINK,1;
		close;
	case 1:
		mes "[フォード]";
		mes "ゲフェンの南に位置する警戒区域に";
		mes "行って「^FF0000スラッティ^000000」という";
		mes "者を探してくれ。";
		next;
		mes "[フォード]";
		mes "彼に会ったら、報告書を貰って";
		mes "私へ持って来れば良い。";
		mes "宜しく頼む！";
		close;
	case 2:
		mes "[フォード]";
		mes "え？　スラッティに会って";
		mes "何も貰ってないのか？";
		mes "早く戻って報告書を貰って来るように！";
		close;
	case 3:
		mes "[フォード]";
		mes "おお〜　来てくれたな。";
		mes "さあ、向こうの状況は？";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "この手紙を……。";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "-スラッティから貰った";
		mes "血が付いた手紙を";
		mes "フォードに手渡した-";
		next;
		mes "[フォード]";
		mes "ん……？";
		next;
		mes "[フォード]";
		mes "-ゴシゴシ-";
		next;
		mes "[フォード]";
		mes "こりゃ……この封筒の血が";
		mes "手につくな……";
		next;
		mes "[フォード]";
		mes "うむ……どうなんだ。";
		mes "-ゴシゴシ-";
		next;
		mes "[フォード]";
		mes "ハアッ!!　今こんな状況なのか！";
		mes "これは大変だ！　早くアルガ様に";
		mes "報告をしなければ！";
		mes "兵力を増強して攻撃だ。";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "あの、ちょっと待ってください。";
		mes "その手紙をくれたスラッティさんが……";
		next;
		mes "[フォード]";
		mes "あ、そうだ。";
		mes "スラッティはどうした。";
		mes "どうして一人で来たのだ。";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "ゴブリンたちに襲撃され、";
		mes "今命が危ないところです。";
		next;
		mes "[フォード]";
		mes "何！　大変だ！";
		mes "しかし、私はこの手紙を今すぐ";
		mes "アルガ様に送って兵力の増強を";
		mes "要請せねばならん。";
		next;
		mes "[フォード]";
		mes "すまないが、お前は今すぐスラッティを";
		mes "助けに行ってくれないか？";
		mes "遅れると命にかかわる！";
		next;
		mes "[フォード]";
		mes "しかし……";
		mes "スラッティがゴブリンに";
		mes "やられたのなら、";
		mes "もう手遅れかもしれんな……。";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "どうしてですか?!";
		next;
		mes "[フォード]";
		mes "ゴブリン達は刀に毒を塗って";
		mes "使うから、ゴブリンの刀による";
		mes "傷は解毒効果のある薬を";
		mes "使わなければ";
		mes "傷が癒えないだろう。";
		next;
		mes "[フォード]";
		mes "もし回復用のポーションを";
		mes "使っていたら、もうそのことには";
		mes "気が付いているだろう。";
		next;
		mes "[フォード]";
		mes "その解毒剤を手に入れるためには";
		mes "プロンテラにある大聖堂の";
		mes "アーサー司祭の所へ行くしかない。";
		mes "だが、ここからは結構遠いから";
		mes "戻るにも時間がかかってしまう。";
		next;
		mes "[フォード]";
		mes "時間がないから早く出発してくれ。";
		mes "私はこの手紙で増兵を要請して、";
		mes "ゴブリン村へ送る。";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "わかりました！";
		mes "-プロンテラ大聖堂の";
		mes "アーサー司祭に";
		mes "早く会わなければならない-";
		set SKILL_SHRINK,4;
		close;
	case 4:
		mes "[フォード]";
		mes "解毒剤はプロンテラ大聖堂の";
		mes "アーサー司祭がくれるだろう。";
		mes "急いでくれ。";
		close;
	case 5:
	case 6:
		mes "[フォード]";
		mes "アーサー司祭に会ったのか？";
		mes "早く薬を用意して";
		mes "スラッティに飲ませるように。";
		close;
	default:
		mes "[フォード]";
		mes "薬を貰って来たようだな。";
		mes "じゃ、早くスラッティの所へ";
		mes "行って助けてやってくれ。";
		close;
	}
}

gef_fild13.gat,297,242,3	script	兵士	751,{
	if(getskilllv(1002)) {
		mes "[スラッティ]";
		mes "おう、旅は順調か？";
		mes "たまにはこっちに来て";
		mes "俺と剣術の練習でもすれば";
		mes "良いのに……。ハハ。";
		close;
	}
	switch(SKILL_SHRINK) {
	case 1:
		mes "[兵士]";
		mes "ウウウ……";
		mes "足が……足が……。";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "どうしたんですか？";
		next;
		mes "[兵士]";
		mes "フォードに状況を";
		mes "報告しに行く途中で";
		mes "ゴブリンたちに襲撃された……。";
		mes "今、ゴブリンの群れが村を";
		mes "襲おうと押しかけている。";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "では、あなたがスラッティさん？";
		next;
		mes "[スラッティ]";
		mes "そう。俺がスラッティだ。";
		mes "お前は俺を知っているのか？";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "フォードさんから頼まれ事を";
		mes "受けているんです。";
		next;
		mes "[スラッティ]";
		mes "そうか。それはちょうど良かった。";
		mes "お前に報告書と軍隊出動要請書を";
		mes "渡すから、早くフォードの所へ";
		mes "行って渡してほしい。";
		next;
		mes "[スラッティ]";
		mes "これを上層部へ報告して";
		mes "兵を送るにはフォードでも時間が";
		mes "かかると思うからな。";
		mes "なるべく早く……たのむ。";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "では、あなたは？";
		mes "こんな状態でどうするつもりですか？";
		next;
		mes "[スラッティ]";
		mes "俺は大丈夫だ。";
		mes "ウウウッ!!";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "本当に大丈夫ですか？";
		next;
		mes "[スラッティ]";
		mes "ハハ。本当に大丈夫。";
		mes "オオオッ！　アア……。";
		mes "足が……。アアッ！";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "……。";
		next;
		mes "[スラッティ]";
		mes "ハハ……心配ないからな〜";
		mes "オウゥ!!!";
		mes "クオオオ〜";
		next;
		mes "[スラッティ]";
		mes "血！ウウッ！";
		mes "ウオオッ！アフゥ……。";
		mes "血が……グホア……。";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "……。";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "-やっぱり赤ポーションを";
		mes "持って来た方が良さそうだ-";
		set SKILL_SHRINK,2;
		close;
	case 2:
		if(countitem(501) < 1) {
			mes "[スラッティ]";
			mes "ウアア……俺の足……。";
			mes "ウウウ……。";
			mes "クオオオオ……。";
			next;
			mes "[" +strcharinfo(0)+ "]";
			mes "-やっぱり赤ポーションを";
			mes "持って来た方が良さそうだ-";
			close;
		}
		mes "[" +strcharinfo(0)+ "]";
		mes "一応ポーションを持って来たので";
		mes "これを飲んでみてください。";
		next;
		mes "[スラッティ]";
		mes "ウウッ。ありがとう……。";
		mes "-ゴクゴク-";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "-状態は少し良くなったが、";
		mes "まだ血が止まらない-";
		next;
		mes "[スラッティ]";
		mes "もう良い。";
		mes "俺はもう駄目だから、";
		mes "早く俺の言うとおりに……";
		mes "手紙を持って行くように……。";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "でも……。";
		next;
		mes "[スラッティ]";
		mes "早く行け！";
		mes "これは俺の命を救うだけで";
		mes "解決する問題ではない。";
		mes "早く、早く行ってくれ！";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "スラッティさん、私、";
		mes "必ず戻って来ますから";
		mes "じっとしていてくださいね。";
		next;
		mes "[スラッティ]";
		mes "その言葉だけで十分だ。";
		mes "ありがとう。";
		mes "だから……もう急いでくれ。";
		mes "ゴホンゴホン。";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "-血が付いている手紙を受け取った-";
		delitem 501,1;
		set SKILL_SHRINK,3;
		close;
	case 3:
		mes "[スラッティ]";
		mes "ここで何をしているんだ！";
		mes "早く行け!!";
		mes "俺が渡した手紙を";
		mes "フォードに渡せ!!";
		close;
	case 4:
		mes "[スラッティ]";
		mes "クウウッ。";
		mes "……。";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "-スラッティはまたゴブリンに";
		mes "襲われたようで、傷が増えている。";
		mes "精神状態も悪そうだ-";
		close;
	case 5:
		mes "[スラッティ]";
		mes "クウウッ。";
		mes "……。";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "-スラッティはまたゴブリンに";
		mes "襲われたようで、傷が増えている。";
		mes "精神状態も悪そうだ。";
		mes "一刻も早く薬が必要な状態だ-";
		close;
	case 6:
		mes "[スラッティ]";
		mes "……。";
		mes "……。";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "-スラッティはまたゴブリンに";
		mes "襲われたようで、傷が増えている。";
		mes "精神状態も悪そうだ。";
		mes "一刻も早く薬が必要な状態だ-";
		close;
	case 7:
		if(rand(5) < 3) {
			mes "[" +strcharinfo(0)+ "]";
			mes "-持って来た薬をスラッティの";
			mes "口に少しずつ注いであげたが、";
			mes "薬が間違っていたのか";
			mes "傷がまったく癒えない。";
			mes "もう一度薬を貰いに行かなければ-";
			set SKILL_SHRINK,8;
			close;
		}
		mes "[" +strcharinfo(0)+ "]";
		mes "-持って来た薬を";
		mes "スラッティの口に少しずつ";
		mes "注いであげた-";
		next;
		mes "[スラッティ]";
		mes "-ゴクゴク-";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "-間に合ったのか";
		mes "スラッティの傷が";
		mes "本当に癒え始めた-";
		next;
		mes "[スラッティ]";
		mes "ウウウム〜";
		mes "クウウッ！";
		mes "どうしたんだ……。";
		mes "俺、生きているのか……。";
		mes "!!!!?";
		next;
		mes "[スラッティ]";
		mes "お、お前は?!";
		next;
		mes "[スラッティ]";
		mes "まさか?!";
		mes "お前が俺を救ってくれたのか？";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "はい。";
		next;
		mes "[スラッティ]";
		mes "そ、そんな……。";
		mes "俺が生きているなんて……。";
		next;
		mes "[スラッティ]";
		mes "気を失う前まで";
		mes "この道を通るやつらは皆";
		mes "俺が倒したんだ……。";
		mes "正直俺はもう死んだと";
		mes "思っていたが、お前が戻って来て";
		mes "助けてくれるとは……。";
		next;
		mes "[スラッティ]";
		mes "そうだ。";
		mes "要請書は無事に渡してくれたか？";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "はい。";
		next;
		mes "[スラッティ]";
		mes "それは幸いだ。";
		mes "うむ……何か……";
		mes "恩返しをしたいんだが……。";
		mes "そうだ、お前に俺の技を伝授してやる。";
		next;
		mes "[スラッティ]";
		mes "俺が一人でここまで耐えることが";
		mes "できたのも、この技のおかげだろう。";
		mes "「^FF0000シュリンク^000000」という技だ。";
		next;
		mes "[スラッティ]";
		mes "「^FF0000シュリンク^000000」は";
		mes "それが発動している状態で";
		mes "「オートガード」を使うと";
		mes "敵の攻撃を防いだ場合、その敵を";
		mes "弾き飛ばすという技なんだ。";
		next;
		mes "[スラッティ]";
		mes "お前の役にも立つはずだ。";
		mes "さあ、早速教えてやろう。";
		mes "俺が命の恩人にしてやれるのが";
		mes "こんなもんですまないが……。";
		next;
		mes "[スラッティ]";
		mes "だが、これからも俺はクルセイダーの";
		mes "新技を体得していくつもりだから、";
		mes "その時はぜひ俺の所に来て欲しい。";
		mes "お前には一番に教えてやるからな。";
		next;
		mes "[スラッティ]";
		mes "俺が今日教えた技を使って、";
		mes "俺を助けてくれた時のように";
		mes "人々を守って欲しい。";
		mes "では、この先の旅にも気をつけて";
		mes "くれよな。じゃ、またな。";
		set SKILL_SHRINK,0;
		skill 1002,1,0;
		close;
	case 8:
		mes "[" +strcharinfo(0)+ "]";
		mes "-どうやら来る途中に";
		mes "薬の効果が薄れてしまったようだ。";
		mes "またアーサー司祭の所へ行って";
		mes "貰って来なければならない-";
		close;
	default:
		mes "[スラッティ]";
		mes "ここはゴブリン達が多く出没する";
		mes "地域だから気をつけるように！";
		close;
	}
}

prt_church.gat,87,127,3	script	司祭	755,{
	if(getskilllv(1002)) {
		mes "[アーサー]";
		mes "こんにちは。";
		mes "お久しぶりですね。";
		mes "また苦しんでいる方が";
		mes "いらっしゃったら、いつでも私に";
		mes "声をかけてください。";
		close;
	}
	switch(SKILL_SHRINK) {
	case 4:
		mes "[アーサー]";
		mes "うむ……？";
		mes "何かご用でしょうか？";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "ある兵士がゴブリンに襲われて、";
		mes "今危険な状況なんです。";
		mes "あなたがゴブリンの毒の解毒剤を";
		mes "持っていると聞いて訪ねました。";
		next;
		mes "[アーサー]";
		mes "ふむ、ゴブリンの毒の解毒剤なら、";
		mes "私が作ることができます。";
		mes "しかし、今その薬を切らしてしまって、";
		mes "作るなら材料が必要ですが……";
		mes "残念ながら、材料も手元に無いんです。";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "その材料って何ですか？";
		mes "私が探して来ます！";
		next;
		mes "[アーサー]";
		mes "材料は……";
		mes "べとべとする液体20個、";
		mes "空きビン3本、ゼロピー5個、";
		mes "まだ熟してないリンゴ1個、";
		mes "ブドウ1個、石炭1個、";
		mes "サイファー3個が必要です。";
		next;
		mes "[アーサー]";
		mes "もし、その方が傷を負ってから";
		mes "かなり時間が経っているのなら、";
		mes "急いだ方が良いでしょう。";
		next;
		mes "[アーサー]";
		mes "そして、もう一つ！";
		mes "この薬が完成したとしても";
		mes "100％効果があるとは言えません。";
		mes "それについては材料をお持ちの際に";
		mes "お話します。";
		next;
		mes "[アーサー]";
		mes "では、お早いお帰りを";
		mes "お待ちしています。";
		mes "私はあなたが戻って来るまで";
		mes "薬を作る準備をしておきます。";
		next;
		mes "[アーサー]";
		mes "もう一度申し上げますが、";
		mes "材料は、べとべとする液体20個、";
		mes "空きビン3本、ゼロピー5個、";
		mes "まだ熟してないリンゴ1個、石炭1個、";
		mes "ブドウ1個、サイファー3個です。";
		mes "数もお忘れなきように。";
		set SKILL_SHRINK,5;
		close;
	case 5:
		if(countitem(938) < 20 || countitem(713) < 3 || countitem(909) < 5 ||
			countitem(619) < 1 || countitem(1003) < 1 || countitem(514) < 1 || countitem(7053) < 3) { 
			mes "[アーサー]";
			mes "ふむ……";
			mes "薬の材料が足りません……";
			next;
			mes "[アーサー]";
			mes "再び申し上げますが、";
			mes "材料は、べとべとする液体20個、";
			mes "空きビン3本、ゼロピー5個、";
			mes "まだ熟してないリンゴ1個、石炭1個、";
			mes "ブドウ1個、サイファー3個です。";
			mes "くれぐれもお間違えなく。";
			close;
		}
		mes "[アーサー]";
		mes "お、材料をお持ちのようですね。";
		mes "それでは、確認してみましょう。";
		mes "うん……うむ……。";
		next;
		mes "[アーサー]";
		mes "-ガサゴソ-";
		mes "うむ……合っていますね。";
		mes "では、すぐに調剤しましょう。";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "あ、そうだ。";
		mes "先ほど戻って来たら";
		mes "教えてくださるとおっしゃっていた";
		mes "問題について教えていただけますか？";
		next;
		mes "[アーサー]";
		mes "ああ、そうでしたね……。";
		mes "この薬は……";
		next;
		mes "[アーサー]";
		mes "作ってからすぐ、でなければ";
		mes "できる限り早く飲まないと";
		mes "薬効が薄れてしまう可能性が";
		mes "あるのです。";
		mes "そうなると、傷も治りません。";
		next;
		mes "[アーサー]";
		mes "普段は患者の方がこちらに";
		mes "訪ねていらっしゃるので";
		mes "特に気をつけていなかったのですが……";
		mes "今回のような場合は";
		mes "少し気をつけなければなりません。";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "分かりました。";
		mes "とりあえず、作ってください。";
		mes "後は私がなんとかします。";
		next;
		mes "[アーサー]";
		mes "薬はさっきお持ちいただいた";
		mes "材料からたくさん作れますので、";
		mes "もしも薬が効かなかった場合は";
		mes "また来てください。";
		mes "すぐにお作りしますよ。";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "はい。分かりました。";
		next;
		mes "[アーサー]";
		mes "それでは調剤をします。";
		mes "少々お待ちください。";
		delitem 938,20;
		delitem 713,3;
		delitem 909,5;
		delitem 619,1;
		delitem 1003,1;
		delitem 514,1;
		delitem 7053,3;
		set SKILL_SHRINK,6;
		close;
	case 6:
		mes "[アーサー]";
		mes "-ブクブク";
		mes "チョロチョロ-";
		next;
		mes "[アーサー]";
		mes "あ、今ちょうど完成するところです。";
		next;
		mes "[アーサー]";
		mes "今私が両手に持っている";
		mes "薬瓶の一つを、もう片方に";
		mes "入れた瞬間から調合が始まります！";
		mes "ここからは早さが命です！";
		next;
		mes "[アーサー]";
		mes "一度薬を混ぜたら";
		mes "もう後戻りはできません。";
		mes "よくご覧になっていてください。";
		mes "それではいきますよ！";
		next;
		mes "[アーサー]";
		mes "この薬を混ぜると";
		mes "赤に変わるはずです。";
		mes "では、これから混ぜます。";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "-アーサー司祭は右手に";
		mes "持っていた瓶の液体を";
		mes "左側の瓶に半分位注いだ-";
		next;
		mes "[アーサー]";
		mes "-チョロチョロ-";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "-色が赤に変わった-";
		next;
		mes "[アーサー]";
		mes "そして、もうしばらくしたら緑色に……";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "-瓶の中の液体が緑に";
		mes "変化しつつ青く光りだした-";
		next;
		mes "[アーサー]";
		mes "ここで残りも混ぜます。";
		next;
		mes "[アーサー]";
		mes "-チョロチョロ-";
		next;
		mes "[アーサー]";
		mes "もうしばらくしたら、";
		mes "最後に水のように";
		mes "透明な液体に変わります。";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "-数秒後、瓶の中の";
		mes "薬が透明になった-";
		mes "-アーサーはその薬瓶を";
		mes "用心深く渡してくれた-";
		next;
		mes "[アーサー]";
		mes "-^FF0000では急いで！^000000-";
		mes "幸運をお祈りしています。";
		set SKILL_SHRINK,7;
		close;
	case 7:
		mes "[アーサー]";
		mes "え？　まだ出発を";
		mes "していなかったのですか。";
		mes "あまり時間が経ってしまうと";
		mes "薬の効果が弱くなるので";
		mes "お急ぎになった方が良いですよ。";
		close;
	case 8:
		mes "[アーサー]";
		mes "薬の効果が薄くなってしまって";
		mes "患者さんに効かなかったようですね……";
		mes "またお作りしますよ。";
		next;
		mes "[アーサー]";
		mes "-サクサク……チョロチョロ-";
		mes "-チョロチョロ-";
		mes "出来上がりました。";
		mes "では、急いで持っていって";
		mes "あげてください!!";
		set SKILL_SHRINK,7;
		close;
	default:
		mes "[アーサー]";
		mes "私はこちらプロンテラ大聖堂の";
		mes "司祭、アーサーと申します。";
		mes "堂内では静かにしてくださいね。";
		close;
	}
}