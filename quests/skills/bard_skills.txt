//===== rAthena Script ======================================= 
//= Bard Skill Quest
//===== By: ================================================== 
//===== Current Version: ===================================== 
//= 1.0
//===== Compatible With: ===================================== 
//= Auriga Project
//===== Description: ========================================= 
//= Official quest for the Bard skill "Pang Voice"
//= Prerequisite: Geffen Bard Quest
//===== Additional Comments: ================================= 
//============================================================

//-----------------------------------------------
//==== パンボイス - BA_PANGVOICE
//	■ SKILL_PANGVOICE   -> 0〜
//-----------------------------------------------
prontera.gat,169,72,7	script	青年#bard	89,3,3,{
	if(getbaseclass(Class,2) != CLASS_BA) {
		mes "[肝が小さい青年]";
		mes "世界は本当に素晴らしい。";
		mes "そうは思わないか？　…………。";
		mes "ハトが何て言ってるかわかればなぁ。";
		mes "…………クルッポー。";
		mes "あ、何の話をしていたっけ。";
		mes "ハハハ……";
		close;
	}
	emotion 23;
	mes "[肝が小さい青年]";
	mes "ヒイイッー！";
	if(Upper == UPPER_HIGH) mes "ク……ク……クラウンだ!!";
	else mes "バ……バ……バードだ!!";
	mes "ち……近寄るな！";
	mes "私はお前たちが";
	mes "嫌いだぁぁぁぁぁ!!!!";
	next;
	if(select("なぜ？　どうして嫌いなんだ!?","落ち着かせよう")==1) {
		mes "[肝が小さい青年]";
		mes "ヒイイイイッ!!";
		mes "わ……私に何をする気だ!?";
		mes "そうだ！　前もそうだった！";
		mes "親しい振りをして近付いてくる!!!!";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "近付いてきた奴は何をしたの？";
		mes "相棒でも見てくれと言ったの？";
		next;
		mes "[肝が小さい青年]";
		mes "そう！　それだ！";
		mes "私はお前らの";
		mes "そのくだらない冗談と";
		mes "その音が嫌いだ!!!!";
		mes "あっちへ行け！";
		close;
	}
	mes "[肝が小さい青年]";
	mes "ヒイイイイッ!!";
	mes "わ……私に何をする気だ!?";
	mes "そうだ！　前もそうだった！";
	mes "親しい振りをして近付いてくる!!!!";
	next;
	if(select("飲み物でも一杯渡す","肩をたたく")==2) {
		mes "[肝が小さい青年]";
		mes "……何だ!?";
		mes "今度は何の呪いをかけるつもりだ!?";
		if(getskilllv(1010))
			mes "……あっち行け！";
		else {
			mes "あ……";
			mes "まだお前はあれができないのか？";
			mes "ホッ……助かったぁ……";
		}
		close;
	}
	if(countitem(12112) < 1) {
		mes "[肝が小さい青年]";
		mes "……何だ？";
		mes "私に何かくれるのか!!!!";
		mes "それなら、私は果物が";
		mes "いっぱい入っているのが好きだ！";
		next;
		mes "[肝が小さい青年]";
		mes "しかし……";
		mes "あれはモロクの特産で、";
		mes "手に入れるのが大変なんだ。";
		mes "……シクシク。";
		next;
		mes "[肝が小さい青年]";
		mes "って、何で私がお前と";
		mes "こんな話をしているんだ!?";
		next;
		mes "[肝が小さい青年]";
		mes "…………";
		mes "アア……";
		mes "トロピカル・ソグラト…………";
		mes "飲みたいな…………";
		close;
	}
	mes "[肝が小さい青年]";
	mes "オオオオオー!!";
	mes "これは私の大好物、";
	mes "トロピカル・ソグラトじゃないか！";
	next;
	if(getskilllv(1010)) {
		mes "[肝が小さい青年]";
		mes "ありがたく頂くよ。";
		mes "ゴクゴクッ…………フォッ!?";
		mes "まさかこれで";
		mes "私の緊張を緩めておいて、";
		mes "またあれを使うつもりなのか!?!?";
		next;
		mes "[肝が小さい青年]";
		mes "あっちへ行ってくれ!!!!";
		close;
	}
	if(SKILL_PANGVOICE) {
		mes "[肝が小さい青年]";
		mes "うん？";
		mes "君は前にも…………";
		mes "そうそう、私に飲み物を";
		mes "買ってくれたいい人だね。";
		next;
		mes "[肝が小さい青年]";
		mes "…………まさか、前に私が言った";
		mes "あれを学びに";
		mes "行くんじゃないだろうね？";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "ハッハッー！";
		mes "そんなまさか〜";
		close;
	}
	mes "[肝が小さい青年]";
	mes "アァァ…………";
	mes "……こんな物を持って来るなんて、";
	mes "一体、私に何の用なんだ？";
	next;
	if(select("一体、何をされたんだ？","おびえてるように見えて……")==2) {
		mes "[肝が小さい青年]";
		mes "私は君たちのせいで";
		mes "とんでもない目にあった！";
		mes "あれは今でもトラウマのように";
		mes "私を苦しめる！";
		next;
	}
	mes "[肝が小さい青年]";
	mes "あれは…………";
	mes "この前、ウンバラに";
	mes "旅行をしに行っていた時だ。";
	mes "ウンバラのシャーマンに";
	mes "何か学びなさいと紹介された";
	mes "吟遊詩人に会ったんだ。";
	next;
	mes "[肝が小さい青年]";
	mes "彼に会った事で、";
	mes "私は恐ろしい経験をした。";
	mes "ウンバラにあるだろ？";
	mes "高い所が………………";
	next;
	mes "[" +strcharinfo(0)+ "]";
	mes "バンジージャンプ台か……";
	mes "高い所は苦手？";
	next;
	mes "[肝が小さい青年]";
	mes "大ッ嫌いだ！";
	mes "ところがあいつは";
	mes "度胸を試してやると言って……";
	next;
	mes "[" +strcharinfo(0)+ "]";
	mes "？　後ろから押したとか？";
	next;
	mes "[肝が小さい青年]";
	mes "そうじゃない！";
	mes "やつが私を睨んで";
	mes "何かぶつぶつと言ってると、";
	mes "急に私の体が";
	mes "勝手に動き始めたんだ!!!!";
	next;
	mes "[肝が小さい青年]";
	mes "高い所からなんとか降りようと";
	mes "もがけばもがくほど、";
	mes "私の体はあちこち自分勝手に動き……";
	mes "そして…………";
	next;
	mes "[" +strcharinfo(0)+ "]";
	mes "飛びおりたわけね…………";
	mes "ハハハッ。";
	next;
	mes "[肝が小さい青年]";
	mes "笑い事か！";
	mes "自分の意志に反して動く、";
	mes "その体の恐ろしさ！";
	mes "経験していない奴には";
	mes "わからない…………";
	next;
	mes "[" +strcharinfo(0)+ "]";
	mes "はい。はい……";
	mes "それにしても……";
	mes "それは本当に不思議な技だね？";
	mes "私が知る吟遊詩人の歌の中に";
	mes "そんなものはないのだが……";
	next;
	mes "[肝が小さい青年]";
	mes "私もあんなのを使うやつは";
	mes "初めて見た！";
	mes "ぶつぶつと言っていた";
	mes "やつの口元は笑っていたんだ！";
	mes "あいつは地獄の獅子だ。獅子！";
	next;
	mes "[肝が小さい青年]";
	mes "…………とにかく、";
	mes "この飲み物はもらうよ。";
	mes "本当にありがとう。";
	mes "若いからって、";
	mes "あんな気持ち悪いやつを";
	mes "探すのはやめた方がいいよ。";
	next;
	if(JobLevel < 40) {
		mes "[肝が小さい青年]";
		mes "その実力じゃあ学ぶことも";
		mes "できないだろうし。";
		mes "少なくとも職業レベルが40以上は";
		mes "ないと無理なんじゃない。";
		delitem 12112,1;
		close;
	}
	mes "[肝が小さい青年]";
	mes "敢えて探すつもりなら止めないけど、";
	mes "どこにいるかは知らないからね。";
	delitem 12112,1;
	set SKILL_PANGVOICE,1;
	close;
OnTouch:
	if(getbaseclass(Class,2) == CLASS_BA)
		emotion 23;
	end;
}

morocc_in.gat,169,72,7	script	陰気なバード#bard	741,3,3,{
	if(getskilllv(1010)) {
		mes "[ライオト]";
		mes "クックックックックックッ。";
		mes "私が教えてやった技術はどうだ？";
		mes "あれは、いい加減に使ってたら";
		mes "私のように憎まれてしまうぞ。";
		mes "クックックックックックッ。";
		next;
		mes "[ライオト]";
		mes "だから";
		mes "ああいう酔っ払いに";
		mes "使ってやるといい……";
		mes "クックックックックックッ。";
		close;
	}
	if(getbaseclass(Class,2) != CLASS_BA) {
		emotion 29;
		mes "[ライオト]";
		mes "……クックックッ。";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "何がそんなに面白いんだろう？";
		next;
		if(GEF_1QUE != 30 && GEF_1QUE != 31) {
			mes "[ライオト]";
			mes "何だ？";
			mes "バードの友達でもない奴と";
			mes "話すことはない。";
			mes "クックックックックック。";
			close;
		}
		mes "[ライオト]";
		mes "ほぅ。" +(GEF_1QUE == 30? "黒": "銀")+ "の紋章を所有しているのか。";
		mes "クックックックックック。";
		mes "あそこを見てみろ。";
		next;
		mes "-吟遊詩人が目配せで示した所を";
		mes "見てみると、二人がお酒を分けていた-";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "別に……変じゃないと思うけど？";
		next;
		mes "[ライオト]";
		mes "近くに行って直接見るといい。";
		mes "クックックックックック。";
		next;
		mes "-吟遊詩人はその内の一人を睨んで、";
		mes "ぶつぶつと何かをつぶやき始めた-";
		close;
	}
	switch(SKILL_PANGVOICE) {
	case 0:
	case 1:
		mes "[ライオト]";
		mes "……クックックッ。";
		emotion 29;
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "何がそんなに面白いんだ？";
		next;
		if(GEF_1QUE != 30 && GEF_1QUE != 31) {
			mes "[ライオト]";
			mes "……私は静かに暮らしたい。";
			mes "……そういえば、";
			mes "紋章もない友達がまだいるな。";
			mes "クックックックックックッ。";
			next;
			mes "[ライオト]";
			mes "お前もバードだが、";
			mes "真正なバードの友達ではない。";
			mes "ゲフェンにいるバードを";
			mes "見た事がないか？";
			next;
			mes "[ライオト]";
			mes "…………クックックッ。";
			mes "同業者同士、話してみたらどうだ？";
			close;
		}
		mes "[ライオト]";
		mes "ほぅ。" +(GEF_1QUE == 30? "黒": "銀")+ "の紋章を所有しているのか。";
		mes "クックックックックック。";
		mes "あそこを見てみろ。";
		next;
		mes "-吟遊詩人が目配せで示した所を";
		mes "見てみると、二人がお酒を分けていた-";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "別に……変じゃないと思うけど？";
		next;
		mes "[ライオト]";
		mes "近くに行って直接見るといい。";
		mes "クックックックックック。";
		next;
		mes "-吟遊詩人はその内の一人を睨んで、";
		mes "ぶつぶつと何かをつぶやき始めた-";
		mes "-すると、その男の行動が";
		mes "おかしくなり始めた-";
		next;
		mes "[少し酔った男]";
		mes "ん？　あ……な、なんだ？";
		mes "どうして私が自分で自分を";
		mes "抱いてんだ!?";
		next;
		mes "[たくさん酔った男]";
		mes "うん？　おあ？";
		mes "お前、自分に抱かれたか？";
		mes "……でないと、ないと。あ？";
		mes "あえ〜？";
		mes "私はこっちであっちに行く〜。";
		next;
		mes "[少し酔った男]";
		mes "あっちに行きたいのに";
		mes "どうしてこっちに？";
		mes "あ、避け！　当た……";
		mes "アイタ！";
		next;
		mes "[たくさん酔った男]";
		mes "イタ！";
		mes "なにするぁぁぁぁぁぁぁ。";
		mes "どうして私のあぶべぁッ！";
		mes "ヒック。";
		next;
		mes "-二人の男は、互いに";
		mes "どこに行くかわからないように";
		mes "行ったり来たりしている-";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "……酷く酔ってますね……";
		next;
		mes "[ライオト]";
		mes "そうではない。";
		mes "これは私の秘術。";
		mes "彼らの意志と違うように動く。";
		mes "精神が混乱したのだよ…………";
		next;
		if(SKILL_PANGVOICE == 0) {
			mes "[" +strcharinfo(0)+ "]";
			mes "はいはい。";
			mes "酔った人々相手にそんな嘘じゃ";
			mes "通じないよ。";
			next;
			mes "[ライオト]";
			mes "……人の話を信じない奴め。";
			mes "お前のような奴は";
			mes "世の中の悪だ!!";
			close;
		}
		mes "[" +strcharinfo(0)+ "]";
		mes "うん？　混乱？";
		mes "勝手に動く……？";
		mes "まさか！";
		mes "以前、ある男にそれを使って";
		mes "バンジージャンプを";
		mes "させたことがありますか!?";
		next;
		mes "[ライオト]";
		mes "なぜそれを知っている？";
		mes "まさか……";
		mes "その話を聞いて";
		mes "私の元に来たのか？";
		next;
		if(select("そうです！　教えてください！","いいえ")==2) {
			mes "[ライオト]";
			mes "……………………";
			mes "…………チッ。";
			mes "つまらん。";
			close;
		}
		mes "[ライオト]";
		mes "そうだな……教えてもいいが……";
		mes "だが……どうするかな。";
		mes "実は、私はこれを";
		mes "ウンバラのシャーマンの呪いから";
		mes "作りあげたものなのだが、";
		mes "とても手に負えなくてな。";
		next;
		mes "[ライオト]";
		mes "それに、声の管理もとても重要だ。";
		mes "うぅむ……どうするか。";
		mes "…………";
		mes "！　そうだ！";
		next;
		mes "[ライオト]";
		mes "昔、ウンバラにある大きな木に";
		mes "登って遊んでいた。";
		mes "そうしたら、変な所へ出た事があった。";
		next;
		mes "[ライオト]";
		mes "そこで「イヒレ」という";
		mes "名のニワトリに会ったが……";
		next;
		mes "[ライオト]";
		mes "そいつの卵が最高だった。";
		mes "クックックックックッ……";
		mes "^4D4DFFイヒレの卵を5つ^000000取ってこれるか？";
		mes "あれは声の調子がよくなる。";
		set SKILL_PANGVOICE,2;
		close;
	default:
		mes "[ライオト]";
		mes "クク……卵はどうした？";
		mes "あいつは足が早いだろう？";
		mes "クックックックックックッ……";
		close;
	case 7:
		if(countitem(574) < 5) {
			mes "[ライオト]";
			mes "なんだ!?";
			mes "卵が足りんではないか!?";
			mes "むぅ……";
			mes "しかたない。";
			mes "それじゃあ、あと";
			mes "^4D4DFFムナック人形^000000を一つもってこい。";
			next;
			mes "[ライオト]";
			mes "ヒェグンというやつが作った";
			mes "ムナック人形は特に出来がいい。";
			mes "クククククク…………";
			if(countitem(574)) {
				mes "一応、取ってきた卵は";
				mes "私がすべてもらっておこう。";
				delitem 574,countitem(574);
			}
			set SKILL_PANGVOICE,8;
			next;
			mes "[ライオト]";
			mes "…………どうしてそんな目で見る!?";
			mes "イヒレの卵が食べたいのか？";
			mes "そうなのか!?";
			mes "……やらんぞ…………";
			close;
		}
		mes "[ライオト]";
		mes "おぉッ！　本当に手に入れて来たな！";
		mes "この純白に輝く卵。";
		mes "まさしくイヒレ！";
		mes "わざわざ呪いをかけて";
		mes "ニブルヘイムに放しておいた";
		mes "かいがある……";
		next;
		mes "[ライオト]";
		mes "よし、それでは早速教えてやろう。";
		break;
	case 8:
		if(countitem(7277) < 1) {
			mes "[ライオト]";
			mes "ムナック人形！　ムナック人形だ！";
			mes "イヒレの卵の次はこいつだ！";
			mes "さっさとムナック人形を持って来い！";
			close;
		}
		mes "[ライオト]";
		mes "オオッ！";
		mes "本当に手に入れて来たな！";
		mes "華麗な針の腕前！";
		mes "均一に満たされた綿！";
		mes "まさしくヒェグンの腕前だ。";
		next;
		mes "[ライオト]";
		mes "クックックッ。";
		mes "褒め言葉はこのくらいにして、";
		mes "さっそく、教えてやろう。";
		mes "クックックックックックッ……";
		next;
		mes "[ライオト]";
		break;
	}
	//スキル習得続き
	mes "この技術は、";
	mes "私がウンバラを旅していた時、";
	mes "シャーマン、ハルモムの呪いから";
	mes "私だけの技術で作ったもの。";
	mes "……クックックックックック……";
	next;
	mes "[ライオト]";
	mes "まず、技をかける相手を睨む。";
	mes "そして、邪念を入れ込めながら";
	mes "寒いジョークを唱える。";
	mes "奇妙な声を出すのが重要だ。";
	next;
	mes "[ライオト]";
	mes "会長は快調!!";
	mes "くりっとした栗！";
	mes "妖怪に用かい？";
	mes "忍者は全部で何人じゃ？";
	mes "飲みすぎは肝臓にいカンゾウ！";
	mes "魚が驚いた。ギョッ！";
	misceffect 295,"";
	if(SKILL_PANGVOICE == 7) delitem 574,5;
	else delitem 7277,1;
	set SKILL_PANGVOICE,0;
	skill 1010,1,0;
	next;
	mes "[ライオト]";
	mes "今の寒いジョークは他の人が";
	mes "呪いだと気づかないように";
	mes "口にしている。";
	mes "今教えてやった技術は";
	mes "「パンボイス」という。";
	next;
	mes "[ライオト]";
	mes "邪念を加えた奇怪な声で";
	mes "相手の精神に衝撃を加える。";
	mes "これで相手は一時的にだが、";
	mes "自分の思うように";
	mes "動くことができない。";
	mes "……クックックックックックッ……";
	next;
	mes "[ライオト]";
	mes "これは、いい加減に使ってたら";
	mes "私のように憎まれてしまうぞ。";
	mes "クックックックックックッ。";
	next;
	mes "[ライオト]";
	mes "だから";
	mes "ああいう酔っ払いに";
	mes "使ってやるといい……";
	mes "クックックックックックッ。";
	close;
OnTouch:
	emotion 29;
	end;
}

niflheim.gat,239,70,1	script	イヒレ#1	800,3,3,{
	emotion 23;
	emotion 23,"";
	mes "[ニワトリ・イヒレ]";
	mes "コケッ？";
	mes "ココココココケーッコ!!";
	next;
	hideonnpc;
	hideoffnpc "イヒレ#"+(strnpcinfo(2)%5+1);
	mes "-ニワトリが驚いて逃げた-";
	if(SKILL_PANGVOICE < 2 || SKILL_PANGVOICE > 6 || rand(3))
		close;
	mes "^4D4DFF-イヒレがいた所で「卵」を見つけた-";
	mes "-「卵」を一つ手に入れた-^000000";
	set SKILL_PANGVOICE,SKILL_PANGVOICE+1;
	getitem 574,1;
	close;
OnTouch:
	emotion 23;
	emotion 23,"";
	mes "[ニワトリ・イヒレ]";
	mes "コケッ？";
	mes "ココココココケーッコ!!";
	next;
	hideonnpc;
	hideoffnpc "イヒレ#"+(strnpcinfo(2)+1);
	mes "-ニワトリが驚いて逃げた-";
	close;
}
niflheim.gat,185,205,3	duplicate(イヒレ#1)	イヒレ#2	800,3,3
niflheim.gat,85,203,5	duplicate(イヒレ#1)	イヒレ#3	800,3,3
niflheim.gat,149,81,7	duplicate(イヒレ#1)	イヒレ#4	800,3,3
niflheim.gat,209,144,3	duplicate(イヒレ#1)	イヒレ#5	800,3,3

morocc_in.gat,178,73,3	script	お客さん#bard	54,{
	mes "[少し酔った男]";
	mes "やはり酒は言葉だ。";
	mes "そこどこか。";
	mes "そこそこそれが一番なの！";
	mes "ト……トリスタン？";
	next;
	mes "[たくさん酔った男]";
	mes "あ〜トリスタン13年製？";
	mes "ヒック。";
	mes "しかし、私たちみたいな庶民が";
	mes "そんな高い酒を飲むことができるか？";
	next;
	mes "[たくさん酔った男]";
	mes "私は……ヒクック。";
	mes "ボミル……ボムリ……";
	mes "浜辺にボトリ……";
	next;
	mes "[少し酔った男]";
	mes "ボミル？";
	mes "ボミルもっと持ってこーうい！";
	mes "グウレ！";
	mes "あれ名品だろう。";
	mes "一杯引っかければ";
	mes "そのまま目の前が真暗になると〜！";
	next;
	mes "[たくさん酔った男]";
	mes "ヒック。";
	mes "ト、トロピカル！";
	mes "あれも素晴らしい〜";
	mes "あ……";
	mes "ほんの少し……";
	next;
	mes "[少し酔った男]";
	mes "ん？　あ……な、なんだ？";
	mes "どうして私が自分で自分を";
	mes "抱いてんだ!?";
	next;
	mes "[たくさん酔った男]";
	mes "うん？　おあ？";
	mes "お前、自分に抱かれたか？";
	mes "……でないと、ないと。あ？";
	mes "あえ〜？";
	mes "私はこっちであっちに行く〜。";
	next;
	mes "[少し酔った男]";
	mes "あっちに行きたいのに";
	mes "どうしてこっちに？";
	mes "あ、避け！　当た……";
	mes "アイタ！";
	next;
	mes "[たくさん酔った男]";
	mes "イタ！";
	mes "なにするぁぁぁぁぁぁぁ。";
	mes "どうして私のあぶべぁッ！";
	mes "ヒック。";
	next;
	mes "[少し酔った男]";
	mes "私が……ヒック……そう！";
	mes "体が自分勝手に動くん……ヒック。";
	mes "酒を……ウィック。";
	mes "いっぱい……ヒィック……のっだか？";
	next;
	mes "-お酒を飲んだ二人の男は、";
	mes "用を足しに行こうと歩き出し、";
	mes "変な方向に動きながら慌てている-";
	next;
	mes "-一方、その光景を見ている";
	mes "吟遊詩人がほくそ笑んでいる-";
	mes "-酒はたくさん飲むのではない-";
	close;
}
morocc_in.gat,175,70,7	duplicate(お客さん#bard)	お客さん	50

morocc_in.gat,166,76,7	script	バーテン	46,{
	mes "[バーテン]";
	mes "注文か？";
	next;
	if(select("トロピカル・ソグラト","ヴァーミリオン・ザ・ビーチ","いいえ")==3){
		mes "[バーテン]";
		mes "そうか……";
		close;
	}
	if(MaxWeight - Weight < 2000) {
		mes "[バーテン]";
		mes "お前、酒を持てるのか？";
		mes "少しふらふらしてるぞ。";
		close;
	}
	if(Zeny < 1000) {
		mes "[バーテン]";
		mes "……金はあるのか？";
		mes "財布を確認してから注文してくれ。";
		mes "一杯1000Zenyだ。";
		close;
	}
	mes "[バーテン]";
	if(@menu == 1) mes "これはほとんどが果汁だが、";
	mes "飲みすぎには気をつけな。";
	set Zeny,Zeny-1000;
	getitem 12111+@menu,1;
	close;
}